
def getInst() (
  return layouts.shared.state.currentDocument.rtrInst
)

def topbar() (
  local inst @= self.getInst()
  
  goto frame.left + 10 0
  square 15 15 0 0 1
  if inst != null (
    if mouse_touching (
      cursor "pointer"
      if onclick (
        inst.console @= []
      )
    )
    c shared.theme.text
  ) else (
    if mouse_touching (
      cursor "not-allowed"
    )
    c shared.theme.seco
  )
  icon "w 2.5 cutcircle 0 0 10 0 180 line -6 -6 6 6" .7
)

def update() (
  local inst @= self.getInst()
  
  local y = frame.top + frame.scroll
  local start = y
  
  for ii inst.console.len (
    local item @= inst.console[ii]
    local lines @= item[2].wrapText(frame.width - 10 / 10).split("\n")
    local height = lines.len * 20
    
    local col = null
    if item[1] == "err" or item[1] == "repl-err" (
      col = #f00
    )
    
    local ly = y
    local sy = y
    
    y -= 5
    y -= height
    y -= 5
    
    if col != null (
      frame frame.left y frame.right ly (
        c col
        pen "opacity" 20
        pen "size" 10000
        pen "down"
        pen "up"
      )
    )
    
    local offsetX = 5
    
    if item[1].startsWith("repl") (
      goto frame.left + offsetX + 5 ly - 15
      
      if item[1] == "repl-in" (
        icon "right" .5 : c#shared.theme.text
      )
      if item[1] == "repl-ret" (
        icon "left" .5 : c#shared.theme.text
      )
      if item[1] == "repl-err" (
        icon "left" .5 : c#shared.theme.text
      )
      offsetX += 20
    )
    
    c shared.theme.text
    for i lines.len (
      ly -= 10
      goto frame.left + offsetX ly - 5
      text lines[i] 10
      ly -= 10
    )
    
    pen "size" 2 : c#shared.theme.prim
    
    goto 0 sy
    line frame.left 0 frame.right 0
    
    goto 0 y
    line frame.left 0 frame.right 0
  )
  
  // repl
  y -= 15
  goto frame.left + 12.5 y
  icon "right" .5 : c#shared.theme.text
  
  local inputId = "devtools_repl_" ++ inst.ouid
  goto 12.5 y
  input frame.width - 35 30 inputId null 0 shared.theme.text : c#shared.theme.back
  
  if "enter".onKeyDown() and inputs.selected.id == inputId (
    local inp = inputs[inputId]
    
    void inst.console.append(["repl-in", inp])
    
    promiseData = inp
    promiseData2 @= inst
    self.runPromise @= Promise.new(def() -> (
      local parser @= rtr.ast.Parser("event(onload){return(" ++ promiseData ++ ");}")
      self.ast @= parser.parse()
      self.mod @= rtr.main.Module(self.ast)
      void promiseData2.addModule(self.mod)
      local ret @= mod.runEventRaw({
        name: "onload"
      }, {})
      return ret
    ))
    
    inputs[inputId] = ""
  )
  
  if self.runPromise != null (
    local ret @= self.runPromise.worker.return
    if ret != null (
      void inst.console.append(["repl-ret", ret.stringify(true)])
      self.runPromise = null
    )
  )
  
  y -= 15
  
  self.height = start - y - 10
)
