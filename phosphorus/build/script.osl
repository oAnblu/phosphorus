opal_imports = {}
def import(path) (
  switch path (

case "./src/devtools/menus/storage.osl"
  class __imports__968375a74382766e626cb371cd10b43a (
    
  )
  return __imports__968375a74382766e626cb371cd10b43a
case "./src/devtools/menus/network.osl"
  class __imports__b4ec205ca81f7630cd7f76511fd744fb (
    
  )
  return __imports__b4ec205ca81f7630cd7f76511fd744fb
case "./src/devtools/menus/console.osl"
  class __imports__ac7f1c744ff0d794c130cdf96196926d (
    def getInst() (
      return layouts.shared.state.currentDocument.rtrInst
    )
    
    def topbar() (
      local inst @= self.getInst()
      
      goto frame.left + 10 0
      square 15 15 0 0 1
      if inst != null (
        if mouse_touching (
          cursor "pointer"
          if onclick (
            inst.console @= []
          )
        )
        c shared.theme.text
      ) else (
        if mouse_touching (
          cursor "not-allowed"
        )
        c shared.theme.seco
      )
      icon "w 2.5 cutcircle 0 0 10 0 180 line -6 -6 6 6" .7
    )
    
    def update() (
      local inst @= self.getInst()
      
      local y = frame.top
      local start = y
      
      for ii inst.console.len (
        local item @= inst.console[ii]
        local lines @= item[2].wrapText(frame.width - 10 / 10).split("\n")
        local height = lines.len * 20
        
        local col = null
        if item[1] == "err" (
          col = #f00
        )
        
        local ly = y
        local sy = y
        
        y -= 5
        y -= height
        y -= 5
        
        if col != null (
          frame frame.left y frame.right ly (
            c col
            pen "opacity" 20
            pen "size" 10000
            pen "down"
            pen "up"
          )
        )
        
        for i lines.len (
          ly -= 10
          goto frame.left + 5 ly - 5
          text lines[i] 10 : c#shared.theme.text
          ly -= 10
        )
        
        pen "size" 2 : c#shared.theme.prim
        
        goto 0 sy
        line frame.left 0 frame.right 0
        
        goto 0 y
        line frame.left 0 frame.right 0
      )
    )
  )
  return __imports__ac7f1c744ff0d794c130cdf96196926d
case "./src/devtools/menus/elements.osl"
  class __imports__7de0f89117b2e081473955ae82bcf430 (
    number indent = 20
    
    number x = 0
    number y = 0
    
    number maxX = 0
    
    def drawElements(array elements) (
      for i elements.len (
        if elements[i] != null (
          drawElement(elements[i])
        )
        if i < elements.len (
          text "," 10
        )
      )
    )
    
    def drawElement(object element) (
      if element.children != null or element.kind == "script" (
        self.y -= 15
        goto 0 self.y
        
        element.dvt_open ??= false
        
        goto self.x self.y
        square 20 20 0 0 1 : c#fff
        if mouse_touching (
          cursor "pointer"
          if onclick (
            element.dvt_open = !element.dvt_open
          )
        )
        icon element.dvt_open ? "down" "right" .5 : c#shared.theme.text
        
        text element.blockName ?? element.name 10 : chx#15
        if mouse_touching (
          cursor "pointer"
          if onclick (
            element.dvt_open = !element.dvt_open
          )
        )
        
        self.y -= 15
        
        if element.dvt_open (
          text "{" 10 : chx#10
          self.maxX = max(self.maxX, x_position)
          self.x += self.indent
          
          if element.kind == "script" (
            local content = element.body
            local lines @= content.split("\n")
            local line_height = 25
            local height = lines.len - 1 * line_height + 5
            
            local cy = self.y - 2.5
            self.y -= height
            
            for i lines.len (
              cy -= line_height / 2
              
              goto self.x cy
              
              self.maxX = max(self.maxX, x_position + (lines[i].len * 10))
              text lines[i].trimText(frame.right - x_position - 17.5 / 8) 10 : c#shared.theme.text
              
              cy -= line_height / 2
            )
          ) else (
            drawElements(element.children)
          )
          
          self.x -= self.indent
          
          self.y -= 15
          goto self.x - 5 self.y
          text "}" 10
          self.y -= 15
        ) else (
          if element.kind == "script" (
            text "{" 10 : chx#10
            drawTooLong() : chx#10
            text "}" 10 : chx#10
          ) else (
            text "{" 10 : chx#10
            drawMiniChildren(element.children) : chx#10
            text "}" 10 : chx#10
          )
          self.maxX = max(self.maxX, x_position)
        )
      ) else (
        self.y -= 15
        goto self.x self.y
        drawValue(element.value)
        self.y -= 15
      )
    )
    
    def drawValue(object value) (
      switch value.type (
        case "str"
          text value.value.JsonStringify() 10
          break
        case "num"
          text value.value 10
          break
      )
    )
    
    def drawMiniChildren(array children) (
      if children.len == 0 (
        change_x -20
        return
      )
      
      change_x 5
      for i children.len (
        drawMiniChild(children[i])
        if i < children.len (
          text ", " 6
        )
      )
      change_x 5
      
      change_x -20
    )
    
    def drawMiniChild(object element) (
      if element.children != null or element.kind == "script" (
        text element.blockName ?? element.name 7
        text "{" 7 : chx#10
        drawTooLong()
        text "}" 7 : chx#20
      ) else (
        text "Element" 7
      )
    )
    
    def drawTooLong() (
      text "..." 8 : chx#5
      change_x -15
    )
    
    def update() (
      local document @= layouts.shared.state.currentDocument
      local elements @= document.rwlInst.elements
      
      local s = frame.scroll_h * -1
      self.x = frame.left + s + 15
      local startX = self.x - 20
      self.y = frame.top + frame.scroll
      local startY = self.y
      
      self.width = 0
      self.maxX = 0
      
      if elements != null (
        drawElements(elements)
      )
      
      self.width = self.maxX - startX
      self.height = startY - self.y + 5
    )
  )
  return __imports__7de0f89117b2e081473955ae82bcf430
case "./src/layouts/summit/topbar.osl"
  class __imports__6a477e7780a631dfc33d414269cd6008 (
    def update(array area) (
      goto rwl.area.centerX(area) rwl.area.centerY(area)
      square rwl.area.width(area) - 10 rwl.area.height(area) - 10 10 : c#shared.theme.back
      
      local btns @= layouts.shared.topbar.defaultButtons
      void layouts.shared.topbar.winButtons(btns, false)
      
      local titlebarX = max(area[1], window.left + 30)
      goto titlebarX + 10 rwl.area.centerY(area)
      if layouts.shared.state.currentDocument != null (
        local icn = layouts.shared.state.currentDocument.getIcon()
        if icn != null (
          icon icn 1 : c#fff chx#7.5
          change_x 17.5
        )
        text layouts.shared.state.currentDocument.getTitle() 10 : c#shared.theme.text
      )
      
      frame area[1] area[4] titlebarX area[2] (
        goto 0 0
        square frame.width - 5 25 0 0 1
        if mouse_touching (
          cursor "pointer"
          if onclick (
            layouts.summit.sidebar.open = true
          )
        )
        icon "right" .6 : c#shared.theme.text chx#5
      )
    )
  )
  return __imports__6a477e7780a631dfc33d414269cd6008
case "./src/layouts/summit/sidebar.osl"
  class __imports__3dfd082eaa91cc9b8340f13c6baac410 (
    open_width = 250
    
    width = 200
    tar_width = open_width
    open = true
    
    def update(array area) (
      goto rwl.area.centerX(area) rwl.area.centerY(area)
      square rwl.area.width(area) - 20 rwl.area.height(area) - 20 10 : c#shared.theme.back
      
      frame area[1] + 10 area[4] - 10 area[3] - 10 area[4] - 35 (
        //square 1000 1000 1000 : c#fff
        goto frame.right - 12.5 0
        square 20 20 0 0 1 : c#fff
        if mouse_touching (
          cursor "pointer"
          if onclick (
            self.open = false
          )
        )
        goto frame.right - 12.5 0
        icon "left" .6 : c#shared.theme.text
        
        goto frame.left + 12.5 0
        square 20 20 0 0 1 : c#fff
        if mouse_touching (
          cursor "pointer"
          if onclick (
            void layouts.shared.state.openEmptyTab()
          )
        )
        icon "add" .6 : c#shared.theme.text
      )
      
      void layouts.shared.urlbar.update([
        area[1] + 10,
        area[4] - 40 - 32.5,
        area[3] - 10,
        area[4] - 40 - 2.5
      ])
      
      local height = 1000
      c shared.theme.prim
      frame area[1] + 5 area[4] - 40 - 35 area[3] - 5 area[2] + 5 height "browser_sidebar" (
        local y = frame.top + frame.scroll
        
        for i layouts.shared.state.tabs.len (
          local tab @= layouts.shared.state.tabs[i]
          
          local title = tab.document.getTitle()
          local icon = tab.document.getIcon()
          y -= 20
          
          local close_touching = false
          
          goto frame.right - 22.5 y
          square 15 15 0 0 1
          local close_touching = mouse_touching
          
          goto 0 y
          c layouts.shared.state.currentTab == i ? shared.theme.seco shared.theme.prim
          square frame.width - 25 20 15 : hover_c#shared.theme.tert
          if mouse_touching and !close_touching (
            cursor "pointer"
            if onclick (
              void layouts.shared.state.selectTab(i)
            )
          )
          square frame.width - 25 20 10 : c#shared.theme.back
          
          goto frame.left + 15 y
          if icon != null (
            icon icon .9 : c#user.theme.text chx#7.5
            change_x 15
          )
          text title.trimText(frame.right - x_position / 9 - 4) 9 : c#shared.theme.text tooltip#title
          
          goto frame.right - 22.5 y
          square 15 15 0 0 1
          if mouse_touching (
            cursor "pointer"
            if onclick (
              void layouts.shared.state.closeTab(i)
              i --
            )
          )
          icon "close" .5
          
          y -= 20
        )
      )
      
      // clicking on the left side of the window opens the sidebar
      if self.width < 25 (
        goto window.left 0
        square 20 window.height 0 0 1
        if mouse_touching (
          cursor "pointer"
          if onclick (
            self.open = true
          )
        )
      )
      
      self.tar_width = self.open ? self.open_width 5
      self.width += self.tar_width - self.width * min(delta_time, 0.02) * 10
    )
  )
  return __imports__3dfd082eaa91cc9b8340f13c6baac410
case "./src/layouts/summit/main.osl"
  class __imports__70bd3fa18cef0c20ca734751135b82d8 (
    def init() (
      void shared.document.init()
      void layouts.shared.state.init()
    )
    
    def update() (
      void shared.theme.load()
      
      window.show()
      window_colour = #000
      
      glass:frame window.left window.top window.right window.bottom; frame "clear"
      
      void layouts.shared.state.resetDragbox()
      
      layouts.shared.state.dragbox[1][3] = max(layouts.summit.sidebar.width, 30)
      
      void layouts.summit.sidebar.update([
        window.left + layouts.summit.sidebar.width - layouts.summit.sidebar.open_width,
        window.bottom,
        window.left + layouts.summit.sidebar.width,
        window.top
      ])
      
      void layouts.summit.topbar.update([
        window.left + layouts.summit.sidebar.width,
        window.top - 37.5,
        window.right - 5,
        window.top - 5
      ])
      
      void layouts.shared.view.render([
        window.left + layouts.summit.sidebar.width,
        window.bottom + 5,
        window.right - 5,
        window.top - 42.5
      ])
      
      void layouts.shared.state.applyDragbox()
    )
  )
  return __imports__70bd3fa18cef0c20ca734751135b82d8
case "./src/layouts/chromeLike/topbar.osl"
  class __imports__0f6f8e5da8ad7dfb8f0386ba0c4b7fd4 (
    def tabs() (
      local space = window.width - 100 - 17.5 - 15
      
      local hovered = false
      
      local x = 0
      local tabMax = space / layouts.shared.state.tabs.len - 5
      for i layouts.shared.state.tabs.len (
        local tab @= layouts.shared.state.tabs[i]
        
        local title = tab.document.getTitle()
        local icon = tab.document.getIcon()
        
        local pad = 15 + 27.5
        if icon != null (
          pad += 20
        )
        //local width = title.len * 9 + pad
        local width = 200
        //width = max(width, 200)
        width = min(width, tabMax)
        
        local lx = x
        x += 2.5 + (width / 2)
        
        loc 2 2 x + (width / 2) - 17.5 -20
        square 15 15 0 0 1
        local close_touching = mouse_touching
        
        loc 2 2 x -20
        
        square width - 17.5 17.5 15 0 1
        c mouse_touching ? shared.theme.seco shared.theme.prim
        if layouts.shared.state.currentTab == i (
          c shared.theme.tert
        )
        square width - 17.5 17.5 15
        
        if mouse_touching and !close_touching (
          layouts.shared.state.dragbox @= [
            [2, 2, lx, 0],
            [2, 2, lx + width, -20]
          ]
          hovered = true
          
          cursor "pointer"
          if onclick (
            void layouts.shared.state.selectTab(i)
          )
        )
        
        square width - 17.5 17.5 10 : c#shared.theme.prim
        
        if icon != null (
          lx += 20
          loc 2 2 lx -20
          icon icon .9 : c#user.theme.text
          lx += 5
        )
        lx += 10
        
        loc 2 2 lx -20
        text title.trimText(width - pad / 9) 9 : c#shared.theme.text tooltip#title
        
        lx += 5
        loc 2 2 x + (width / 2) - 17.5 -20
        square 15 15 0 0 1
        if mouse_touching (
          cursor "pointer"
          if onclick (
            void layouts.shared.state.closeTab(i)
            i --
          )
        )
        icon "close" .5
        
        x += 2.5 + (width / 2)
      )
      
      x += 17.5
      loc 2 2 x -20
      square 17.5 17.5 15 : c#shared.theme.prim hover_c#shared.theme.seco
      if mouse_touching (
        cursor "pointer"
        if onclick (
          void layouts.shared.state.openEmptyTab()
        )
      )
      icon "add" .6 : c#user.theme.text
      x += 20
      
      loc -2 2 -90 -20
      line 0 -20 0 20 : c#shared.theme.prim w#1
      
      if !hovered (
        layouts.shared.state.dragbox[1][3] += x
      )
    )
  )
  return __imports__0f6f8e5da8ad7dfb8f0386ba0c4b7fd4
case "./src/layouts/chromeLike/main.osl"
  class __imports__34355e72215dbff81e1963b1534949ee (
    def init() (
      void shared.document.init()
      void layouts.shared.state.init()
    )
    
    def update() (
      void shared.theme.load()
    
      window.show()
      window_colour = shared.theme.back
      
      void layouts.shared.state.resetDragbox()
    
      local btns @= layouts.shared.topbar.defaultButtons
      void layouts.shared.topbar.winButtons(btns, false)
      
      void layouts.chromeLike.topbar.tabs()
      
      void layouts.shared.urlbar.update([
        window.left,
        window.top - 45 - 30,
        window.right,
        window.top - 44
      ])
      
      void layouts.shared.view.render([
        window.left,
        window.bottom,
        window.right,
        window.top - 45 - 32.5
      ])
      
      void layouts.shared.state.applyDragbox()
    )
  )
  return __imports__34355e72215dbff81e1963b1534949ee
case "./src/layouts/shared/view.osl"
  class __imports__5228a5cd2bf6a46e3a340119920c2e17 (
    def render(array area) (
      local devtoolsData @= devtools.main ?? { width: 0, anchor: "l" }
      
      local devtoolsArea @= rwl.area.copy(area)
      local contentArea @= rwl.area.copy(area)
      switch devtoolsData.anchor (
        case "l"
          contentArea[1] += devtoolsData.width
          devtoolsArea[3] = contentArea[1]
          break
        case "r"
          contentArea[3] -= devtoolsData.width
          devtoolsArea[1] = contentArea[3]
          break
      )
      
      devtoolsArea[1] += 5
      
      if layouts.shared.state.currentDocument != null (
        void layouts.shared.state.currentDocument.checkResp()
        void layouts.shared.state.currentDocument.render(contentArea)
      )
      
      if typeof(devtools) == "object" (
        void devtools.main.update(devtoolsArea, area)
      )
    )
  )
  return __imports__5228a5cd2bf6a46e3a340119920c2e17
case "./src/layouts/shared/urlbar.osl"
  class __imports__e1f3f683350eb46be2bb39ac4a8762b8 (
    inputId = "browser_url"
    
    def update(array area) (
      local defaultText = "blehh"
      
      goto rwl.area.centerX(area) rwl.area.centerY(area) + 2.5
      
      local w = rwl.area.width(area) - 15
      local h = rwl.area.height(area) - 0
      
      square w - 5 h - 18 15 : c#shared.theme.prim
      input w h - 5 self.inputId defaultText 0 shared.theme.text
      
      if inputs.selected.id == self.inputId (
        if "enter".onKeyDown() (
          void self.goto()
        )
      )
      
      //void shared.graphics.box(area, #f00)
    )
    
    def goto() (
      local url = inputs[self.inputId]
      
      url @= shared.url.Url(url)
      
      local tab @= layouts.shared.state.tabs[layouts.shared.state.currentTab]
      void tab.redirect(url)
    )
  )
  return __imports__e1f3f683350eb46be2bb39ac4a8762b8
case "./src/layouts/shared/topbar.osl"
  class __imports__bce8e403422ca1f602a36f77e9c471c6 (
    array defaultButtons = [
      {
        icon: "close",
        click: window.close
      },
      {
        icon: "down",
        click: window.minimise
      },
      {
        icon: "maximise",
        click: window.fullscreen
      }
    ]
    
    def winButtons(array buttons, boolean background) (
      if background (
        local w = buttons.len * 25
        
        loc -2 2 w / -2 - 7.5 -20
        square w - 5 18 10 : c#shared.theme.prim
      )
      
      loc -2 2 -20 -20
      change_x 25
      for i buttons.len (
        local button @= buttons[i]
        
        c shared.theme.text
        icon button.icon 0.6 * (button.icon_size ?? 1) : hover_size#1.1 chx#-25
        if mouse_touching (
          cursor "pointer"
          if onclick (
            void button.click()
          )
        )
      )
    )
  )
  return __imports__bce8e403422ca1f602a36f77e9c471c6
case "./src/layouts/shared/utils.osl"
  class __imports__bd7a86c2700e28469496d9f67248c0b2 (
    def getContentArea() (
      return [
        window.left,
        window.bottom,
        window.right,
        window.top - 50
      ]
    )
  )
  return __imports__bd7a86c2700e28469496d9f67248c0b2
case "./src/layouts/shared/state.osl"
  class __imports__d1763f06b30a5d5d9c10608cf0f00c3e (
    debug = false
    
    def init() (
      void self.updateTab()
      void self.selectTab(1)
      
      self.tabs @= []
    )
    
    def resetDragbox() (
      self.dragbox @= [[2,2,0,0],[-2,2,-90,-40]]
    )
    
    def applyDragbox() (
      window.setDragbox(self.dragbox[1], self.dragbox[2])
      
      if self.debug (
        c #fff
        loc self.dragbox[1][1] self.dragbox[1][2] self.dragbox[1][3] self.dragbox[1][4]
        pen "down"
        loc self.dragbox[2][1] self.dragbox[2][2] self.dragbox[2][3] self.dragbox[2][4]
        pen "up"
      )
    )
    
    def openTabDocument(object document) (
      local tab @= shared.tab.Tab(document)
      void self.tabs.append(tab)
      void self.selectTab(self.tabs.len)
    )
    
    def openEmptyTab() (
      void self.openTabDocument(shared.document.Document())
    )
    
    def closeTab(number index) (
      void self.tabs[index].document.close()
      void self.tabs.delete(index)
      
      if index < self.currentTab (
        self.currentTab --
      )
      
      void self.selectTab(self.currentTab)
    )
    
    def selectTab(number index) (
      self.currentTab @= index
      void self.updateTab()
      inputs[layouts.shared.urlbar.inputId] = self.currentDocument.url.text
    )
    
    def updateTab() (
      self.currentDocument @= self.tabs[self.currentTab].document
      //self.currentDocument ??= shared.document.Document()
    )
  )
  return __imports__d1763f06b30a5d5d9c10608cf0f00c3e
case "./src/devtools/topbar.osl"
  class __imports__3261b0e52c0051dec0dc6394c943d033 (
    tabs_scroll = 0
    tabs_width = 0
    
    def update() (
      goto 0 -5
      square frame.width - 10 frame.height 10 : c#shared.theme.prim
      
      square frame.width frame.height 0 0 1
      if mouse_touching (
        self.tabs_scroll += scroll.x.velocity + scroll.y.velocity * scroll.multiplier * 1.5
      )
      
      self.tabs_scroll = min(self.tabs_scroll, self.tabs_width - frame.width)
      self.tabs_scroll = max(self.tabs_scroll, 0)
      
      local x = frame.left - self.tabs_scroll
      local start = x
      local menus @= devtools.menus.getEntries()
      for i menus.len (
        local menu @= menus[i]
        
        local title = menu[1].toTitle()
        local w = title.len * 8 + 10
        
        x += w / 2
        
        goto x 0
        square w - 10 10 10 0 1
        c devtools.main.menu == menu[1] ? shared.theme.seco shared.theme.prim
        if mouse_touching (
          c shared.theme.tert
          cursor "pointer"
          if onclick (
            devtools.main.menu = menu[1]
          )
        )
        pen "size" 2
        line w / -2 -11 w / 2 -11
        centext title 8 : c#shared.theme.text
        
        x += w / 2
      )
      self.tabs_width = x - start
    )
  )
  return __imports__3261b0e52c0051dec0dc6394c943d033
case "./src/devtools/main.osl"
  class __imports__87f5572a1b07abb6f61e525f0557da7a (
    string anchor = "r"
    number width = 350
    number resizingEdge = 0
    
    menu = "console"
    
    def update(array area, array rawArea) (
      goto rwl.area.centerX(area) rwl.area.centerY(area)
      square rwl.area.width(area) - 10 rwl.area.height(area) - 10 10 : c#shared.theme.back
      
      local h = 25
      frame area[1] area[4] area[3] area[4] - h (
        void devtools.topbar.update()
      )
      
      if mouse_y > area[2] and mouse_y < area[4] (
        if anchor == "r" and abs(mouse_x - area[1]) < 5 (
          cursor "col-resize"
          goto mouse_x mouse_y
          square 100 100 0 0 1
          if onclick (
            self.resizingEdge = 1
          )
        )
        if anchor == "l" and abs(mouse_x - area[3]) < 5 (
          cursor "col-resize"
          goto mouse_x mouse_y
          square 100 100 0 0 1
          if onclick (
            self.resizingEdge = 3
          )
        )
      )
      
      if !mouse_down (
        self.resizingEdge = 0
      )
      
      if self.resizingEdge != 0 (
        self.width = abs(mouse_x - area[4 - self.resizingEdge])
      )
      self.width = min(self.width, rwl.area.width(rawArea) - 200)
      self.width = max(self.width, 200)
      
      local menu @= devtools.menus[self.menu]
      
      c shared.theme.prim
      local topbarH = 0
      if menu.topbar != null (
        topbarH = 25
        frame area[1] area[4] - h area[3] area[4] - h - topbarH (
          void menu.topbar()
        )
        local w = rwl.area.width(area)
        goto rwl.area.centerX(area) area[4] - h - topbarH
        pen "size" 2
        line w / -2 -1 w / 2 -1 : c#shared.theme.prim
        topbarH += 1
      )
      
      c shared.theme.prim
      frame area[1] area[4] - h - topbarH area[3] area[2] [menu.width,menu.height] "devtools_" ++ self.menu (
        if menu != null (
          if menu.update != null (
            void menu.update()
          )
        ) else (
          goto 0 0
          centext "no open devtools menu" 9 : c#shared.theme.prim
        )
      )
    )
  )
  return __imports__87f5572a1b07abb6f61e525f0557da7a
case "./src/net/response.osl"
  class __imports__f336b41d073413cbdabb122f71c530f0 (
    class Response (
      isValid = false
      
      def init(object url, string content) (
        self.url @= url
        self.content = content
        
        self.isFinished = true
      )
    )
    
    class NoResponse extends Response (
      
    )
    
    class ValidResponse extends Response (
      isValid = true
      
      def init(object url) (
        self.url @= url
        
        self.isFinished = false
      )
      
      def update() (
        void self.fetch()
      )
    )
  )
  return __imports__f336b41d073413cbdabb122f71c530f0
case "./src/net/fetch.osl"
  class __imports__d974ce5692d372884f5c4f8f8bf1ab18 (
    def url(object url) (
      if url.scheme == "local" (
        local f @= open(url.domain_name).toStr()
        local r @= net.response.ValidResponse(url)
        r.fetch @= def() -> (
          self.content = self.f
          self.isFinished = true
        )
        r.f @= f
        return r
      )
    
      local servers @= shared.config.net.servers
      local server @= servers[url.scheme]
      
      if server == null (
        local r @= net.response.NoResponse(url, "scheme '" ++ url.scheme ++ "' not found")
        return r
      )
      
      local resource = url.domain_top
      resource ++= "/"
      if url.domain_sub != null (
        resource ++= url.domain_sub
        resource ++= "."
      )
      resource ++= url.domain_name
      if url.resource != null (
        resource ++= "/"
        resource ++= url.resource
      )
      
      local tld = server.tlds[url.domain_top]
      
      if tld == null (
        local r @= net.response.NoResponse(url, "tld '" ++ url.domain_top ++ "' not found")
        return r
      )
      
      local realUrl = tld ++ "/" ++ resource
      
      local r @= net.response.ValidResponse(url)
      r.fetch @= def() -> (
        local o = self.realUrl.getAsync()
        if o != "Loading" and o != "404: Not Found" (
          self.content = o
          self.isFinished = true
        )
      )
      r.realUrl = realUrl
      
      return r
    )
  )
  return __imports__d974ce5692d372884f5c4f8f8bf1ab18
case "./src/shared/tab_worker.osl"
  class __imports__ed11349f32bd7389fac05d89b56e016d (
    def oncreate() (
      
    )
    
    def onframe() (
      if self.parseText != null (
        local parser @= rwl.ast.Parser(self.parseText)
        if self != null (
          //log self
          void self.document.loadAst(parser.parse())
          self.parseText = null
        )
      )
    )
  )
  return __imports__ed11349f32bd7389fac05d89b56e016d
case "./src/shared/config.osl"
  class __imports__9f198a2aa8925b082e5f887a2bba9b67 (
    class net (
      servers = {
        //rtr: {
        //  url: "http://localhost:3000/",
        //  
        //  // <server>/<command>
        //  commands: {
        //    tlds: "tlds"
        //  }
        //}
      }
      
      def refresh() (
        for i self.servers.len (
          local s @= self.servers.getValues()[i]
          s.tlds @= (s.url ++ s.commands.tlds).httpGet()
        )
      )
    )
    
    net.refresh()
    
    class url (
      class defaults (
        scheme = "rtr"
        name = "unknown"
        top = "web"
      )
    )
  )
  return __imports__9f198a2aa8925b082e5f887a2bba9b67
case "./src/shared/theme.osl"
  class __imports__5876b7f6fcd5ce7f0fafd1a44ec4bd74 (
    def load() (
      self.back = user.theme.background
      self.prim = user.theme.primary
      self.seco = user.theme.secondary
      self.tert = user.theme.tertiary
      self.text = user.theme.text
    )
    
    load()
  )
  return __imports__5876b7f6fcd5ce7f0fafd1a44ec4bd74
case "./src/shared/url.osl"
  class __imports__a1d00c2cd890aa6e92598446ff24f924 (
    class Url (
      def init(string text) (
        void self.parse(text)
      )
      
      def parse(string text) (
        self.text = text
        local s @= text.match("/^((\\w+):\\/\\/)/")
        local match @= text.match("/^((\\w+):\\/\\/)?(((\\w+)\\.)?(\\w+)(\\.(\\w+))?\\/?(([\\/w]+)?(\\/(\\w+\\.\\w+))?))\\/?/")
        
        self.scheme = match[3] ?? s[3] ?? shared.config.url.defaults.scheme
        
        if self.scheme == "local" (
          self.domain_name = text.trim("local://".len + 1, -1)
          local usrPath = "origin/(c) users/" ++ username
          if self.domain_name.toLower().startsWith(usrPath.toLower()) (
            self.domain_name = "~" ++ self.domain_name.trim(usrPath.len + 1, -1)
          )
          self.domain_top = null
          self.domain_sub = null
          self.path = null
          self.params @= {}
          self.resource = null
          self.text = "local://" ++ self.domain_name
          return
        )
        
        if match[9] != null (
          local domain_name = match[7]
          local domain_top = match[9]
          local domain_sub = match[6]
        ) else (
          if match[6] != null (
            local domain_name = match[6]
            local domain_top = match[7]
            local domain_sub = null
          ) else (
            local domain_name = match[7]
            local domain_top = null
            local domain_sub = null
          )
        )
        
        self.domain_name = domain_name ?? shared.config.url.defaults.name
        self.domain_top = domain_top ?? shared.config.url.defaults.top
        self.domain_sub = domain_sub
        self.path = match[13]
        self.params @= {}
        self.resource = match[10] ?? "index.rwl"
      )
      
      def format() (
        local sub = self.domain_sub != null ? self.domain_sub ++ "." ""
        local top = self.domain_top != null ? "." ++ self.domain_top ""
        local txt = self.scheme ++ "://" ++ sub ++ domain_name ++ top
        if self.path != null (
          txt ++= "/" ++ self.path
        )
        if self.file_name != "index.osl" (
          txt ++= "/" ++ self.file_name
        )
        return txt
      )
      
      def getTitle() (
        if self.scheme == "local" (
          return self.domain_name.split("/")[-1].split(".")[1]
        )
        return self.domain_name
      )
    )
  )
  return __imports__a1d00c2cd890aa6e92598446ff24f924
case "./src/shared/tab.osl"
  class __imports__0ffc38803b2c8e6e0623c0ea1ffbcc28 (
    class Tab (
      def init(object document) (
        self.document @= document
      )
      
      def redirect(object url) (
        void self.document.loadUrl(url)
      )
    )
  )
  return __imports__0ffc38803b2c8e6e0623c0ea1ffbcc28
case "./src/shared/document.osl"
  class __imports__af13446f70bd7836009892ffef4b5078 (
    class Document (
      def init() (
        void self.createInsts()
        
        void self.createWorker()
      )
      
      def close() (
        void self.killWorker()
      )
      
      def createWorker() (
        //log "create worker"
        void killWorker()
        self.worker @= worker(shared.tab_worker)
        self.worker.document @= self
      )
      
      def killWorker() (
        if self.worker != null (
          //log "kill worker"
          void self.worker.kill()
          self.worker = null
        )
      )
      
      def createInsts() (
        self.title = "New Tab"
        if self.url != null (
          self.title = self.url.getTitle()
        )
        self.icon = null
        
        self.rtrInst @= rtr.main.RTR()
        self.rwlInst @= rwl.main.RWL(shared.document.empty, self.rtrInst)
      )
      
      def updateInsts() (
        // inject rwl apis
        void rtr.rwl.addToInst(self, self.rtrInst)
        
        // start onloads
        void self.rtrInst.startModules()
      )
      
      def update(array area) (
        void self.rwlInst.update(area)
      )
      
      def render(array area) (
        if self.worker != null (
          self.worker.area @= area
          if !self.worker.alive (
            self.rwlInst.errored = true
            self.rwlInst.errormsg = "tab worker died, check code for syntax errors\nyou can also check js console"
          )
        )
        
        goto rwl.area.centerX(area) rwl.area.centerY(area)
        square rwl.area.width(area) - 10 rwl.area.height(area) - 10 10 : c#shared.theme.back
        
        if self.loading (
          goto rwl.area.centerX(area) rwl.area.centerY(area)
          direction timer * 720
          icon "sync" .75 : c#shared.theme.text
          direction 90
          return
        )
        
        //log self.rwlInst
        void self.rwlInst.render(area)
      )
      
      def checkResp() (
        if self.resp == null (
          self.loading = false
          return
        )
        
        if self.resp.isFinished (
          if self.resp.isValid (
            self.loading = false
            
            void self.createInsts()
            void self.loadText(self.resp.content)
            
            self.resp = null
          )
        ) else (
          self.loading = true
          void self.resp.update()
        )
      )
      
      def loadAst(object ast) (
        void self.createInsts()
        void self.rwlInst.loadFromAst(ast)
        void self.updateInsts()
      )
      
      def loadText(string text) (
        void self.createWorker()
        self.worker.parseText = text
      )
      
      def loadUrl(object url) (
        self.url @= url
        void self.createInsts()
        self.resp @= net.fetch.url(url)
      )
      
      def getIcon() (
        return self.icon
      )
      def getTitle() (
        return self.title
      )
    )
    
    def init() (
      local parser @= rwl.ast.Parser(import("./src/assets/empty.rwl"))
      self.empty @= parser.parse()
    )
  )
  return __imports__af13446f70bd7836009892ffef4b5078
case "./src/shared/utils.osl"
  class __imports__ace3e4fea1b83d3f4a6cf8da09fee544 (
    def removeIndents(str) (
      local lines @= str.split("\n")
      local indent = null
      for i lines.len (
        local l2 = lines[i].match("/^( *)(.+)$/")
        if l2[2].len > 0 (
          indent = min(indent ?? 9999, l2[2].len)
        )
      )
      for i lines.len (
        lines[i] = lines[i].trim(indent + 1, -1)
      )
      return lines.join("\n")
    )
    
    def jsFunc(string js) (
      local f @= js.eval()
      local f2 @= () -> ()
      f2.code @= f
      return f2
    )
  )
  return __imports__ace3e4fea1b83d3f4a6cf8da09fee544
case "./src/shared/graphics.osl"
  class __imports__8840a5e065c53e817186a2216f863bca (
    usesCanvas = false
    
    _canvasID = null
    _canvas = null
    
    def init() (
      if self.usesCanvas (
        self._canvasID = OuidNew()
        self._canvas @= shared.canvas.createCanvas(self._canvasID, window.width, window.height)
        log self
      )
    )
    
    // cursor
    cursor_x = 0
    cursor_y = 0
    def goto(number x, number y) (
      self.cursor_x = x
      self.cursor_y = y
    )
    
    def change_x(number x) (
      self.cursor_x += x
    )
    def change_y(number y) (
      self.cursor_y += y
    )
    def change(number x, number y) (
      self.cursor_x += x
      self.cursor_y += y
    )
    
    // utils
    def getTextWidth(string text) (
      return text.len
    )
    
    // rendering
    def box(array area, string color) (
      c color
      pen "size" 1
      goto area[1] area[2]
      pen "down"
      goto area[3] area[2]
      goto area[3] area[4]
      goto area[1] area[4]
      goto area[1] area[2]
      pen "up"
    )
    
    def filledBox(array area, rounding, string color) (
      local x = rwl.area.centerX(area)
      local y = rwl.area.centerY(area)
      local w = rwl.area.width(area)
      local h = rwl.area.height(area)
      
      c color
      
      if typeof(rounding) == "number" (
        goto x y
        square w - (rounding * 2) h - (rounding * 2) (rounding * 2)
      ) else if typeof(rounding) == "array" (
        // could rework this to have it be corner - rounding for pos
        // so it would have higher parity with canvas?
        goto x y
        frame area[1] y x area[2] (
          local r = min(rounding[1] * 2, min(w, h) / 2)
          square w - r h - r r
        )
        frame x y area[3] area[2] (
          local r = min(rounding[2] * 2, min(w, h) / 2)
          square w - r h - r r
        )
        frame area[1] area[4] x y (
          local r = min(rounding[3] * 2, min(w, h) / 2)
          square w - r h - r r
        )
        frame x area[4] area[3] y (
          local r = min(rounding[4] * 2, min(w, h) / 2)
          square w - r h - r r
        )
      )
    )
    
    def ctext(string text, number size, string color) (
      void ptext(text, self.cursor_x, self.cursor_y, size, color)
    )
    def ptext(string text, number x, number y, number size, string color) (
      goto x y
      text text size : c#color
    )
  )
  return __imports__8840a5e065c53e817186a2216f863bca
case "./src/rtr/value.osl"
  class __imports__b2506e9c8a1f71d53780de64c11f25a1 (
    class RTRValue (
      boolean isReturnVal = false
      def callFunc() (
        throw "cannot call" + self.stringify(true) + "as function"
      )
    
      def getRtrType() (
        throw self.name + "has not implemented getRtrType()"
        return rtr.value.RTRTypeValue({
          name: "?"
        })
      )
    
      def stringify() (
        return "<" ++ self.getRtrType().getName() ++ ">"
      )
      def numbify() (
        return NaN
      )
      def boolify() (
        return true
      )
      def arrify() (
        return []
      )
      def length() (
        return 0
      )
      def getItem(key) (
        return null
      )
      def setItem(key, value) (
        throw "cannot set items on" + self.stringify(true)
      )
      def keys() (
        return []
      )
      def values() (
        return []
      )
      def has(key) (
        return false
      )
      def isEqual(other) (
        return self.getRtrType().isEqual(other.getRtrType())
      )
    )
    
    class RTRTypeValue (
      def init(object data) (
        self.data @= data
      )
      
      def stringify() (
        return self.getName()
      )
      def getName() (
        if self.data.name != null (
          return self.data.name
        )
        return "?"
      )
      def isEqual(object other) (
        return self.data.name == other.data.name
      )
    )
    
    class RTRLiteralValue extends RTRValue (
      boolean isLiteral = true
    )
    
    class RTRStrValue extends RTRLiteralValue (
      def init(string value) (
        self.value @= value
      )
      
      def getRtrType() (
        return rtr.value.RTRTypeValue({
          name: "str"
        })
      )
      
      def stringify(format) (
        return format ? self.value.JsonStringify() self.value
      )
      def numbify() (
        return self.value.toNum()
      )
      def boolify() (
        return self.value.len > 0
      )
      def arrify() (
        return self.value.split("").map(e -> rtr.value.RTRStrValue(e))
      )
      def length() (
        return self.value.len
      )
      def getItem(index) (
        return rtr.value.RTRStrValue(self.value[index.numbify() + 1])
      )
      def isEqual(other) (
        return self.getRtrType().isEqual(other.getRtrType()) and self.value === other.value
      )
    )
    
    class RTRNumValue extends RTRLiteralValue (
      def init(number value) (
        self.value @= value
      )
      
      def getRtrType() (
        return rtr.value.RTRTypeValue({
          name: "num"
        })
      )
      
      def stringify() (
        return self.value.toStr()
      )
      def numbify() (
        return self.value
      )
      def boolify() (
        return self.value > 0
      )
      def arrify() (
        return self.value.toStr().split("").map(e -> rtr.value.RTRStrValue(e))
      )
      def length() (
        return self.value.toStr().length
      )
      def getItem(index) (
        return self.value.toStr()[index.numbify() + 1]
      )
      def isEqual(other) (
        return self.getRtrType().isEqual(other.getRtrType()) and self.value === other.value
      )
    )
    
    class RTRBoolValue extends RTRLiteralValue (
      def init(boolean value) (
        self.value @= value
      )
      
      def getRtrType() (
        return rtr.value.RTRTypeValue({
          name: "bool"
        })
      )
      
      def stringify() (
        return self.value.toStr()
      )
      def numbify() (
        return self.value.toNum()
      )
      def boolify() (
        return self.value
      )
      def isEqual(other) (
        return self.getRtrType().isEqual(other.getRtrType()) and self.value === other.value
      )
    )
    
    class RTRFuncValue extends RTRValue (
      def getRtrType() (
        return rtr.value.RTRTypeValue({
          name: "func"
        })
      )
    )
    
    class RTROslFuncValue extends RTRFuncValue (
      def init(func) (
        self.func @= func
      )
      
      def callFunc(object inst, array args) (
        return self.func(args, inst) ?? rtr.value.RTRNullValue()
      )
    )
    
    class RTRAstFuncValue extends RTRFuncValue (
      def init(array args, object body) (
        self.args @= args
        self.body @= body
      )
      
      def callFunc(object inst, array args) (
        local s @= {}
        for i self.args.len (
          local a @= self.args[i]
          local v @= args[i] ?? rtr.value.RTRNullValue()
          s[a.name] @= [v, {}]
        )
        
        void inst.newScope(s)
        void inst.declScopeVar("@return", 0)
        
        local exprOut @= inst.runStatement(self.body)
        
        local ret @= inst.getScopeVar("@return") ?? 0
        if ret == 0 (
          ret @= exprOut
        )
        
        void inst.popScope()
        
        return ret
      )
    )
    
    class RTRNullValue extends RTRValue (
      def getRtrType() (
        return rtr.value.RTRTypeValue({
          name: "null"
        })
      )
      
      def boolify() (
        return false
      )
      def isEqual(other) (
        return self.getRtrType().isEqual(other.getRtrType()) and self.value === other.value
      )
    )
    
    class RTRArrValue extends RTRValue (
      def init(array elements) (
        self.elements @= elements
      )
      
      def getRtrType() (
        return rtr.value.RTRTypeValue({
          name: "arr"
        })
      )
      
      def stringify() (
        local el @= self.elements.map(e -> e.stringify(true))
        return "[" ++ el.join(", ") ++ "]"
      )
      def boolify() (
        return self.elements.len > 0
      )
      def arrify() (
        return self.elements
      )
      def length() (
        return self.elements.len
      )
      def keys() (
        local arr2 @= []
        for i self.elements.len (
          void arr2.append(i - 1)
        )
        return arr2
      )
      def values() (
        return self.elements
      )
      def getItem(index) (
        return self.elements[index.numbify() + 1]
      )
    )
    
    class RTRObjValue extends RTRValue (
      def init(object value) (
        self.value @= value
      )
      
      def getRtrType() (
        return rtr.value.RTRTypeValue({
          name: "obj"
        })
      )
      
      def stringify() (
        local el @= self.value.getEntries().map(e -> e[1] ++ ":" + e[2].stringify(true))
        return "{" ++ el.join(", ") ++ "}"
      )
      def boolify() (
        return self.value.getKeys().len > 0
      )
      def arrify() (
        return self.value.getKeys().map(e -> rtr.value.RTRStrValue(e))
      )
      def length() (
        return self.value.getKeys().len
      )
      def keys() (
        return self.value.getKeys()
      )
      def values() (
        return self.value.getValues()
      )
      def has(key) (
        return self.value.getKeys().contains(key)
      )
      def getItem(key) (
        return self.value[key.stringify()]
      )
      def setItem(key, value) (
        self.value[key.stringify()] @= value
      )
    )
  )
  return __imports__b2506e9c8a1f71d53780de64c11f25a1
case "./src/rtr/error.osl"
  class __imports__a51b04c580d521af176787ba84843c9a (
    object Editors = {
    
    }
    
    class Error (
      boolean isError = true
      
      def init(string type, object data) (
        self.type = type
        self.data @= data
        
        local err @= rwl.error.Errors[type]
        if err == null (
          self.txt = "unknown error type" + type
          return
        )
        self.txt = err(data)
      )
      
      def stringify() (
        return "Error<" ++ self.type ++ ">:\n" ++ self.txt.split("\n").map(l -> "    " ++ l).join("\n")
      )
    )
  )
  return __imports__a51b04c580d521af176787ba84843c9a
case "./src/rtr/ast.osl"
  class __imports__2691a00e099698fa56c4748f2551eb65 (
    class Parser (
      def init(string text) (
        void self.tokenise(text)
      )
      
      def parse() (
        self.pointer = 1
        
        local out @= self.blockContent(true)
        
        if !self.atEnd() (
          throw "unexpected token" + self.peek()
        )
        
        return { elements: out }
      )
      
      def peek(amount) (
        amount ??= 1
        return self.tokens[self.pointer + amount - 1]
      )
      def consume() (
        local tkn = self.peek()
        self.pointer ++
        return tkn
      )
      def consumeWhitespace() (
        while self.peek().match("/^\\s$/") != null (
          void self.consume()
        )
      )
      def atEnd() (
        return self.pointer >= self.tokens.len
      )
      def expect(string token) (
        local tkn @= self.consume()
        if tkn != token (
          throw "expected '" ++ token ++ "' got '" ++ tkn ++ "'"
        )
        return tkn
      )
      def expects(array tokens) (
        local tkn @= self.consume()
        if !tokens.contains(tkn) (
          throw "expected '" ++ tokens.join(" or ") ++ "' got '" ++ tkn ++ "'"
        )
        return tkn
      )
      def expectText() (
        local tkn @= self.consume()
        if !tkn.match("/^[a-zA-Z_]+$/") (
          throw "identifier must consist of letters or _"
        )
        return tkn
      )
      def getPos() (
        local ln = 1
        local col = 0
        local i = self.pointer
        for t self.pointer (
          col ++
          if self.tokens[t] == "\n" (
            ln ++
            col = 0
          )
        )
        return { ln, col, i }
      )
      
      def tokenise(string text) (
        self.tokens @= []
        local splitChars @= [
          "(",")",
          "[","]",
          "{","}",
          ",",";",":","=",".",
          "+","-","*","/","%","^",
          "\\",
          "'","\"","`",
          " ","\n",
          "!","?"
        ]
        local buf = ""
        for i text.len (
          if splitChars.contains(text[i]) (
            if buf.len > 0 (
              void self.tokens.append(buf)
            )
            void self.tokens.append(text[i])
            buf = ""
          ) else (
            buf ++= text[i]
          )
        )
        if buf.len > 0 (
          self.tokens.append(buf)
        )
      )
      
      // statements
      def blockContent(top) (
        local statements @= []
        local end = peek() == "}"
        while !end (
          local out @= top ? self.topLevelStatement() self.statement()
          if out != null (
            void statements.append(out)
          )
          void self.consumeWhitespace()
          if self.peek() == ";" (
            void self.consume()
          )
          if self.peek() == "}" or self.atEnd() (
            end = true
          )
        )
        return statements
      )
      
      def topLevelStatement() (
        void self.consumeWhitespace()
        
        if self.peek() == "event" (
          return self.event()
        )
        
        throw "unexpected token '" ++ self.peek() ++ "'"
      )
      
      def event() (
        local start @= self.getPos()
        void self.expect("event")
        void self.consumeWhitespace()
        void self.expect("(")
        void self.consumeWhitespace()
        local name @= self.expectText()
        void self.consumeWhitespace()
        void self.expect(")")
        void self.consumeWhitespace()
        local body @= self.block()
        
        return {
          kind: "event",
          name,
          body,
          start, end: self.getPos()
        }
      )
      
      def statement(noSemicolon) (
        void self.consumeWhitespace()
        
        // statements
        if self.peek() == "{" (
          local start @= self.getPos()
          local body @= self.block()
          return {
            kind: "block",
            body,
            start, end: self.getPos()
          }
        )
        
        if self.peek() == "if" (
          local start @= self.getPos()
          void self.consume()
          void self.consumeWhitespace()
          void self.expect("(")
          local ifCond @= self.expression()
          void self.expect(")")
          void self.consumeWhitespace()
          local ifBody @= self.statement()
          
          local elifs @= []
          
          void self.consumeWhitespace()
          while self.peek() == "elif" (
            void self.consume()
            void self.consumeWhitespace()
            void self.expect("(")
            local elifCond @= self.expression()
            void self.expect(")")
            void self.consumeWhitespace()
            local elifBody @= self.statement()
            
            void elifs.append({
              cond: elifCond,
              body: elifBody
            })
            void self.consumeWhitespace()
          )
          
          local elseBody = null
          void self.consumeWhitespace()
          if self.peek() == "else" (
            void self.consume()
            void self.consumeWhitespace()
            local elseBody @= self.statement()
          )
          
          return {
            kind: "branch",
            cond: ifCond,
            body: ifBody,
            elifs,
            elseBody,
            start, end: self.getPos()
          }
        )
        
        if ["while","until"].contains(self.peek()) (
          local start @= self.getPos()
          local kind @= self.consume()
          void self.consumeWhitespace()
          void self.expect("(")
          local cond @= self.expression()
          void self.expect(")")
          void self.consumeWhitespace()
          local body @= self.statement()
          
          return {
            kind,
            cond,
            body,
            start, end: self.getPos()
          }
        )
        
        if self.peek() == "repeat" (
          local start @= self.getPos()
          void self.consume()
          void self.consumeWhitespace()
          void self.expect("(")
          local amount @= self.expression()
          void self.expect(")")
          void self.consumeWhitespace()
          local body @= self.statement()
          
          return {
            kind: "repeat",
            amount,
            body,
            start, end: self.getPos()
          }
        )
        
        if self.peek() == "for" (
          local start @= self.getPos()
          void self.consume()
          void self.consumeWhitespace()
          void self.expect("(")
          local var @= self.expectText()
          void self.consumeWhitespace()
          void self.expect(",")
          void self.consumeWhitespace()
          local arr @= self.expression()
          void self.expect(")")
          void self.consumeWhitespace()
          local body @= self.statement()
          return {
            kind: "for",
            var,
            arr,
            body,
            start, end: self.getPos()
          }
        )
        
        local expr @= self.expression()
        if expr != null (
          void self.consumeWhitespace()
          if !noSemicolon (
            void self.expect(";")
          )
          
          return {
            kind: "expression",
            expr
          }
        ) else (
          throw "unexpected token '" ++ self.peek() ++ "'"
        )
      )
      
      def block() (
        local start @= self.getPos()
        void self.expect("{")
        void self.consumeWhitespace()
        local elements @= self.blockContent()
        void self.consumeWhitespace()
        void self.expect("}")
        return { elements, start, end: self.getPos() }
      )
      
      // expressions
      
      def expression() (
        return self.declare()
      )
      
      def declare() (
        if self.peek().match("/^[a-zA-Z_]\\w*$/") != null (
          local start @= self.getPos()
          local ptr = self.pointer
          local tar = self.consume()
          void self.consumeWhitespace()
          if self.peek() == ":" and self.peek(2) == "=" (
            void self.consume()
            void self.consume()
            void self.consumeWhitespace()
            
            local val @= self.expression()
            
            return {
              kind: "decl",
              tar,
              val,
              start, end: self.getPos()
            }
          )
          self.pointer = ptr
        )
        
        return self.assignment()
      )
      
      def assignment() (
        local start @= self.getPos()
        local expr @= self.equality()
        void self.consumeWhitespace()
        
        local op = null
        
        if self.peek() ++ self.peek(2) == "??" and self.peek(3) == "=" (
          op = self.peek() ++ self.peek(2)
        )
        
        if self.peek(2) == "=" and ["+","-","*","/","%","^"].contains(self.peek()) (
          op = self.peek()
        )
        
        if op != null or self.peek() == "=" (
          if op != null (
            loop op.len (
              void self.consume()
            )
          )
          void self.consume()
          void self.consumeWhitespace()
          
          local val @= self.expression()
          
          return {
            kind: "asi",
            op,
            tar: expr,
            val,
            start, end: self.getPos()
          }
        )
        
        return expr
      )
      
      def equality() (
        local expr @= self.comparison()
        void self.consumeWhitespace()
        
        while ["==","!="].contains(self.peek() ++ self.peek(2)) (
          local op @= self.consume() ++ self.consume()
          local right @= self.comparison()
          expr @= {
            kind: "binary",
            op,
            left: expr,
            right
          }
        )
        
        return expr
      )
      
      def comparison() (
        local expr @= self.term()
        
        local end = false
        while !end (
          void self.consumeWhitespace()
          if [">","<"].contains(self.peek()) (
            local op @= self.consume()
            local isEql = self.peek() == "="
            if isEql (
              op ++= self.consume()
            )
            
            local right @= self.factor()
            
            expr @= {
              kind: "binary",
              op,
              left: expr,
              right
            }
          ) else (
            end = true
          )
        )
        
        return expr
      )
      
      def term() (
        local expr @= self.factor()
        void self.consumeWhitespace()
        
        while ["+","-"].contains(self.peek()) and self.peek(2) != "=" (
          local op @= self.consume()
          local right @= self.factor()
          expr @= {
            kind: "binary",
            op,
            left: expr,
            right
          }
          void self.consumeWhitespace()
        )
        
        return expr
      )
      
      def factor() (
        local expr @= self.otherBinary()
        void self.consumeWhitespace()
        
        while ["*","/"].contains(self.peek()) and self.peek(2) != "=" (
          local op @= self.consume()
          local right @= self.otherBinary()
          expr @= {
            kind: "binary",
            op,
            left: expr,
            right
          }
          void self.consumeWhitespace()
        )
        
        return expr
      )
      
      def otherBinary() (
        local expr @= self.coalescence()
        void self.consumeWhitespace()
        
        while ["%","^"].contains(self.peek()) and self.peek(2) != "=" (
          local op @= self.consume()
          local right @= self.coalescence()
          expr @= {
            kind: "binary",
            op,
            left: expr,
            right
          }
          void self.consumeWhitespace()
        )
        
        return expr
      )
      
      def coalescence() (
        local expr @= self.unary()
        void self.consumeWhitespace()
        
        while self.peek() ++ self.peek(2) == "??" and self.peek(3) != "=" (
          local op @= self.consume() ++ self.consume()
          local right @= self.unary()
          expr @= {
            kind: "binary",
            op,
            left: expr,
            right
          }
          void self.consumeWhitespace()
        )
        
        return expr
      )
      
      def unary() (
        if self.peek(2) != "=" (
          switch self.peek() (
            case "-"; case "+"; case "!"; case "?"
              return {
                kind: "unary",
                type: self.consume(),
                expr: self.expression()
              }
          )
        )
        
        return self.callExpr()
      )
      
      def callExpr() (
        local start @= self.getPos()
        local expr @= self.func()
        
        if self.peek() == "(" (
          self.consume()
          
          local args @= []
          local end = self.peek() == ")"
          while !end (
            local out @= self.expression()
            if out != null (
              void args.append(out)
            )
            void self.consumeWhitespace()
            if self.peek() == "," (
              void self.consume()
            )
            void self.consumeWhitespace()
            if self.peek() == ")" or self.atEnd() (
              end = true
            )
          )
          void self.expect(")")
          
          return {
            kind: "call",
            args,
            func: expr,
            start, end: self.getPos()
          }
        )
        
        return expr
      )
      
      def func() (
        local start @= self.getPos()
        
        local is = false
        if self.peek() == "(" (
          local end = false
          local depth = 0
          while !end (
            local tkn @= self.consume()
            if tkn == "(" (
              depth ++
            )
            if tkn == ")" (
              depth --
            )
            
            if tkn == ")" and depth == 0 (
              end = true
              is = true
            )
            if self.atEnd() (
              end = true
            )
          )
        )
        void self.consumeWhitespace()
        if is and self.peek() == "~" (
          self.pointer = start.i
          
          local args @= []
          void self.consume()
          void self.consumeWhitespace()
          local end = self.peek() == ")"
          if self.peek() == ")" (
            void self.consume()
          )
          while !end (
            local tkn @= self.expectText()
            void args.append({
              name: tkn
            })
            
            void self.consumeWhitespace()
            local ntkn = self.expects([",",")"])
            void self.consumeWhitespace()
            if ntkn == ")" or self.atEnd() (
              end = true
            )
          )
          void self.consumeWhitespace()
          void self.expect("~")
          void self.consumeWhitespace()
          local body @= self.statement(true)
          return {
            kind: "func",
            args,
            body,
            start, end: self.getPos()
          }
        )
        self.pointer = start.i
        
        return self.objProperty()
      )
      
      def objProperty() (
        local expr @= self.primary()
        void self.consumeWhitespace()
        
        while (self.peek() == "[" or self.peek() == ".") and !self.atEnd() (
          if self.peek() == "[" (
            void self.consume()
            local key @= self.expression()
            void self.expect("]")
          ) else if self.peek() == "." (
            void self.consume()
            local key @= self.expectText()
          ) else (
            throw "unexpected token '" ++ self.peek() ++ "'"
          )
          expr @= {
            kind: "prop",
            obj: expr,
            key
          }
          void self.consumeWhitespace()
        )
        
        return expr
      )
      
      def primary() (
        void self.consumeWhitespace()
        
        if self.peek() == "(" (
          void self.consume()
          local expr @= self.expression()
          void self.expect(")")
          return expr
        )
        
        if self.peek() == "[" (
          return self.arr()
        )
        
        if self.peek() == "{" (
          return self.obj()
        )
        
        if ["'","\"","`"].contains(self.peek()) (
          return self.str()
        )
        
        if self.peek().match("/^[0-9]+$/") != null (
          return self.num()
        )
        
        if self.peek().match("/^[a-zA-Z_]\\w*$/") != null (
          local start @= self.getPos()
          return {
            kind: "var",
            name: self.consume(),
            start, end: self.getPos()
          }
        )
        
        throw "unexpected token '" ++ self.peek() ++ "'"
      )
      
      def obj() (
        void self.expect("{")
        void self.consumeWhitespace()
        
        local pairs @= []
        
        local end = self.peek() == "}"
        while !end (
          void self.consumeWhitespace()
          local name @= self.expectText()
          void self.consumeWhitespace()
          void self.expect(":")
          void self.consumeWhitespace()
          local value @= self.expression()
          
          void pairs.append({ name, value })
          
          void self.consumeWhitespace()
          if self.peek() == "}" (
            end = true
          ) else (
            void self.expect(",")
          )
        )
        void self.expect("}")
        
        return {
          kind: "obj",
          pairs
        }
      )
      
      def arr() (
        void self.expect("[")
        
        local elems @= []
        
        local end = self.peek() == "]"
        while !end (
          local expr @= self.expression()
          void elems.append(expr)
          
          void self.consumeWhitespace()
          if self.peek() == "]" or self.atEnd() (
            end = true
          ) else (
            void self.expect(",")
          )
          void self.consumeWhitespace()
        )
        
        void self.expect("]")
        
        return {
          kind: "arr",
          elems
        }
      )
      
      def str() (
        local start @= self.getPos()
        local q @= self.expects(["'","\"","`"])
        local content = ""
        local end = false
        while (!end) (
          local tkn @= self.peek()
          if tkn == "\\" (
            void self.consume()
            local tkn = self.consume()
            local char = tkn[1]
            switch char (
              case "n"
                char = "\n"
                break
            )
            content ++= char ++ tkn.trim(2,-1)
            continue
          )
          
          if q == tkn (
            end = true
          ) else (
            content ++= self.consume()
          )
          if self.atEnd() (
            end = true
          )
        )
        void self.expect(q)
        return {
          kind: "str",
          val: content,
          start, end: self.getPos()
        }
      )
      
      def num() (
        if self.peek().match("/^[0-9]+$/") == null (
          throw "unexpected token '" ++ self.peek() ++ "'"
        )
        
        local start @= self.getPos()
        local val @= self.consume()
        
        if self.peek() == "." (
          val ++= self.consume()
          if self.peek().match("/^[0-9]+$/") == null (
            throw "unexpected token '" ++ self.peek() ++ "'"
          )
          val ++= self.consume()
        )
        
        val @= val.toNum()
        
        return {
          kind: "num",
          val,
          start, end: self.getPos()
        }
      )
    )
  )
  return __imports__2691a00e099698fa56c4748f2551eb65
case "./src/rtr/main.osl"
  class __imports__954b63a69b7120a02fa83923726ef587 (
    class Module (
      def init(object ast) (
        void self.loadFromAst(ast)
        void self.initScope()
        void self.defScope()
      )
      def loadFromAst(object ast) (
        self.ast @= ast
        
        self.events @= {}
        self.eventList @= []
        for i self.ast.elements.len (
          local topLevel @= self.ast.elements[i]
          if topLevel.kind == "event" (
            if self.eventList.contains(topLevel.name) (
              throw topLevel.name + "event already exists on module"
            )
            void self.eventList.append(topLevel.name)
            self.events[topLevel.name] @= topLevel
          )
        )
      )
      
      def initScope() (
        self.scope @= []
      )
      def newScope(object vars) (
        void self.scope.append(vars)
      )
      def popScope() (
        return self.scope.pop()
      )
      def defScope() (
        // helper funcs
        local cFunc @= def(name, content) -> (
          void self.setScopeVar(name, rtr.value.RTROslFuncValue(content))
        )
        
        void self.newScope({})
        
        void self.setScopeVar("true", rtr.value.RTRBoolValue(true))
        void self.setScopeVar("false", rtr.value.RTRBoolValue(false))
        
        cFunc("log", def(args, inst) -> (
          local txt = args.map(a -> a.stringify()).join(" ")
          log txt
          void inst.inst.console.append(["log", txt])
        ))
        cFunc("error", def(args, inst) -> (
          local txt = args.map(a -> a.stringify()).join(" ")
          void inst.inst.console.append(["err", txt])
          throw txt
        ))
        cFunc("return", def(args, inst) -> (
          local val @= args[1]
          val ??= rtr.value.RTRNullValue()
          void inst.setScopeVar("@return", val)
        ))
        cFunc("typeof", def(args) -> (
          local val @= args[1] ?? rtr.value.RTRNullValue()
          return val.getRtrType()
        ))
        cFunc("length", def(args) -> (
          return rtr.value.RTRNumValue(args[1].length())
        ))
        
        // mathematical
        cFunc("min", def(args) -> (
          local v = args[1].numbify()
          for i args.len - 1 (
            local a @= args[i + 1].numbify()
            v = min(v, a)
          )
          return rtr.value.RTRNumValue(v)
        ))
        cFunc("max", def(args) -> (
          local v = args[1].numbify()
          for i args.len - 1 (
            local a @= args[i + 1].numbify()
            v = max(v, a)
          )
          return rtr.value.RTRNumValue(v)
        ))
        
        cFunc("abs", def(args) -> (
          return rtr.value.RTRNumValue(args[1].numbify().abs())
        ))
        cFunc("sqrt", def(args) -> (
          return rtr.value.RTRNumValue(args[1].numbify().sqrt())
        ))
        
        cFunc("round", def(args) -> (
          return rtr.value.RTRNumValue(args[1].numbify().round())
        ))
        cFunc("floor", def(args) -> (
          return rtr.value.RTRNumValue(args[1].numbify().floor())
        ))
        cFunc("ceil", def(args) -> (
          return rtr.value.RTRNumValue(args[1].numbify().ceiling())
        ))
        
        // string
        cFunc("join", def(args) -> (
          local txt = ""
          for i args.len (
            txt ++= args[i].stringify()
          )
          return rtr.value.RTRStrValue(txt)
        ))
        cFunc("split", def(args) -> (
          return rtr.value.RTRArrValue(args[1]
            .stringify()
            .split(args[2].stringify())
            .map(e -> rtr.value.RTRStrValue(e)))
        ))
        
        cFunc("chr", def(args) -> (
          return rtr.value.RTRStrValue(args[1].numbify().chr())
        ))
        cFunc("ord", def(args) -> (
          return rtr.value.RTRNumValue(args[1].stringify().ord())
        ))
        
        // array
        cFunc("item", def(args) -> (
          return args[1].getItem(args[2])
        ))
        cFunc("range", def(args) -> (
          return rtr.value.RTRArrValue((args[1].numbify() to args[2].numbify()).map(e -> rtr.value.RTRNumValue(e)))
        ))
        
        // object
        cFunc("keys", def(args) -> (
          return rtr.value.RTRArrValue(args[1].keys().map(e -> rtr.value.RTRStrValue(e)))
        ))
        cFunc("values", def(args) -> (
          return rtr.value.RTRArrValue(args[1].values())
        ))
        cFunc("has", def(args) -> (
          return rtr.value.RTRBoolValue(args[1].has(args[2].stringify()))
        ))
        cFunc("obj", def() -> (
          return rtr.value.RTRObjValue({})
        ))
        
        // logical
        cFunc("all", def(args) -> (
          return rtr.value.RTRBoolValue(args.every(a -> a.boolify()))
        ))
        cFunc("any", def(args) -> (
          return rtr.value.RTRBoolValue(args.some(a -> a.boolify()))
        ))
        cFunc("not", def(args) -> (
          return rtr.value.RTRBoolValue(!args[1].boolify())
        ))
      )
      def getScopeVarRaw(string key) (
        for rawI self.scope.len (
          local layer @= self.scope[self.scope.len - rawI + 1]
          if layer[key] != null (
            return layer[key]
          )
        )
        return null
      )
      def getScopeVar(string key) (
        return self.getScopeVarRaw(key)[1]
      )
      def getScopeVarMeta(string key) (
        return self.getScopeVarRaw(key)[2]
      )
      def setScopeVar(string key, val, meta) (
        meta ??= {}
        for rawI self.scope.len (
          local layer @= self.scope[self.scope.len - rawI + 1]
          if layer[key] != null (
            layer[key] @= [val, layer[key][2] ++ meta]
            return
          )
        )
        void self.declScopeVar(key, val, meta)
      )
      def declScopeVar(string key, val, meta) (
        self.scope[self.scope.len][key] @= [val, meta]
      )
      
      def runEvent(string name, object data) (
        if !self.eventList.contains(name) (
          return
        )
        
        local ev @= self.events[name]
        void self.runBlock(ev.body)
      )
      def runBlock(object block) (
        local elements @= block.elements
        local outVal @= rtr.value.RTRNullValue()
        void self.newScope({})
        for i elements.len (
          local out @= self.runStatement(elements[i])
          if self.getScopeVar("@return") ?? 0 != 0 (
            break
          )
        )
        void self.popScope()
        return outVal
      )
      def runStatement(object statement) (
        switch statement.kind (
          case "expression"
            return self.runExpression(statement.expr)
          case "block"
            return self.runBlock(statement.body)
          
          case "branch"
            if self.runExpression(statement.cond).boolify() (
              return self.runStatement(statement.body)
            )
            for i statement.elifs.len (
              local elif @= statement.elifs[i]
              if self.runExpression(elif.cond).boolify() (
                return self.runStatement(elif.body)
              )
            )
            if statement.elseBody != null (
              return self.runStatement(statement.elseBody)
            )
            return rtr.value.RTRNullValue()
          
          case "while"
            local iters = 0
            while self.runExpression(statement.cond).boolify() and self.getScopeVar("@return") ?? 0 == 0 and iters < 999999 (
              void self.runStatement(statement.body)
              iters ++
            )
            return rtr.value.RTRNullValue()
          case "until"
            local iters = 0
            while !self.runExpression(statement.cond).boolify() and self.getScopeVar("@return") ?? 0 == 0 and iters < 999999 (
              void self.runStatement(statement.body)
              iters ++
            )
            return rtr.value.RTRNullValue()
          case "repeat"
            loop self.runExpression(statement.amount).numbify() (
              void self.runStatement(statement.body)
            )
            return rtr.value.RTRNullValue()
          case "for"
            local arr @= self.runExpression(statement.arr).arrify()
            for i arr.len (
              void self.newScope({})
              void self.declScopeVar(statement.var, arr[i])
              void self.runStatement(statement.body)
              void self.popScope()
            )
            return rtr.value.RTRNullValue()
          
          default
            throw "unknown statement type" + statement.kind
        )
      )
      def runExpression(object expression) (
        switch expression.kind (
          case "call"
            local func @= self.runExpression(expression.func)
            local args @= []
            for i expression.args.len (
              void args.append(self.runExpression(expression.args[i]))
            )
            return func.callFunc(self, args) ?? rtr.value.RTRNullValue()
          case "decl"
            local val @= self.runExpression(expression.val)
            
            void self.declScopeVar(expression.tar, val, {
              type: val.getRtrType()
            })
            
            return val
          case "asi"
            local val @= self.runExpression(expression.val)
            
            local existingMeta = null
            local existing = null
            switch expression.tar.kind (
              case "var"
                existingMeta @= self.getScopeVarMeta(expression.tar.name)
                existing @= self.getScopeVar(expression.tar.name)
                break
              case "prop"
                existing @= self.runExpression(expression.tar)
                break
            )
            
            if expression.op != null (
              val @= self.runOperation(existing, expression.op, val)
            )
            
            switch expression.tar.kind (
              case "var"
                if existingMeta != null (
                  local curType @= val.getRtrType()
                  if existingMeta.type != null (
                    if !existingMeta.type.isEqual(curType) (
                      throw "cannot assign" + curType.stringify() + "to" + existingMeta.type.stringify() + "variable" 
                    )
                  )
                )
                void self.setScopeVar(expression.tar.name, val)
                break
              case "prop"
                local obj @= self.runExpression(expression.tar.obj)
                local key @= expression.tar.key
                if typeof(key) == "string" (
                  key @= rtr.value.RTRStrValue(key)
                ) else (
                  key @= self.runExpression(key)
                )
                obj.setItem(key, val)
                break
              default
                throw "cannot assign to" + expression.tar.kind
            )
            
            return val
          case "unary"
            local val @= self.runExpression(expression.expr)
            switch expression.type (
              case "-"
                return rtr.value.RTRNumValue(-val.numbify())
              case "+"
                return rtr.value.RTRNumValue(val.numbify())
              case "!"
                return rtr.value.RTRBoolValue(!val.boolify())
              case "?"
                return rtr.value.RTRBoolValue(val.boolify())
            )
            return rtr.value.RTRNullValue()
          case "binary"
            local left @= self.runExpression(expression.left)
            local right @= self.runExpression(expression.right)
            return self.runOperation(left, expression.op, right)
          case "var"
            local val @= self.getScopeVar(expression.name)
            return val ?? rtr.value.RTRNullValue()
          case "prop"
            local obj @= self.runExpression(expression.obj)
            local key @= expression.key
            if typeof(key) == "string" (
              key @= rtr.value.RTRStrValue(key)
            ) else (
              key @= self.runExpression(key)
            )
            return obj.getItem(key) ?? rtr.value.RTRNullValue()
          
          case "str"
            return rtr.value.RTRStrValue(expression.val)
          case "num"
            return rtr.value.RTRNumValue(expression.val)
          case "func"
            return rtr.value.RTRAstFuncValue(expression.args, expression.body)
          case "arr"
            return rtr.value.RTRArrValue(expression.elems.map(e -> self.runExpression(e)))
          case "obj"
            local val @= {}
            for i expression.pairs.len (
              local pair @= expression.pairs[i]
              val[pair.name] @= self.runExpression(pair.value)
            )
            return rtr.value.RTRObjValue(val)
          
          default
            throw "unknown expression type" + expression.kind
        )
      )
      def runOperation(object left, op, object right) (
        local numT @= rtr.value.RTRNumValue.getRtrType()
        switch op (
          case "+"
            if left.getRtrType().isEqual(numT) and right.getRtrType().isEqual(numT) (
              return rtr.value.RTRNumValue(left.numbify() + right.numbify())
            )
            return rtr.value.RTRStrValue(left.stringify() ++ right.stringify())
          case "-"
            if left.getRtrType().isEqual(numT) and right.getRtrType().isEqual(numT) (
              return rtr.value.RTRNumValue(left.numbify() - right.numbify())
            )
            return rtr.value.RTRNumValue(NaN)
          case "*"
            if left.getRtrType().isEqual(numT) and right.getRtrType().isEqual(numT) (
              return rtr.value.RTRNumValue(left.numbify() * right.numbify())
            )
            return rtr.value.RTRNumValue(NaN)
          case "/"
            if left.getRtrType().isEqual(numT) and right.getRtrType().isEqual(numT) (
              return rtr.value.RTRNumValue(left.numbify() / right.numbify())
            )
            return rtr.value.RTRNumValue(NaN)
          case "%"
            if left.getRtrType().isEqual(numT) and right.getRtrType().isEqual(numT) (
              return rtr.value.RTRNumValue(left.numbify() % right.numbify())
            )
            return rtr.value.RTRNumValue(NaN)
          case "^"
            if left.getRtrType().isEqual(numT) and right.getRtrType().isEqual(numT) (
              return rtr.value.RTRNumValue(left.numbify() ^ right.numbify())
            )
            return rtr.value.RTRNumValue(NaN)
          
          case "=="
            return rtr.value.RTRBoolValue(left.isEqual(right))
          case "!="
            return rtr.value.RTRBoolValue(left.isEqual(right))
          
          case ">"
            return rtr.value.RTRBoolValue(left.numbify() > right.numbify())
          case "<"
            return rtr.value.RTRBoolValue(left.numbify() < right.numbify())
          case ">="
            return rtr.value.RTRBoolValue(left.numbify() >= right.numbify())
          case "<="
            return rtr.value.RTRBoolValue(left.numbify() <= right.numbify())
        
          case "??"
            return left.getRtrType().data.name == "null" ? right left
        )
        
        return rtr.value.RTRNullValue()
      )
    )
    
    class RTR (
      def init() (
        self.modules @= {}
        self.console @= []
      )
    
      def addModule(object mod) (
        local key @= OuidNew()
        mod.inst @= self
        self.modules[key] @= mod
        return key
      )
      
      def startModules() (
        local mods @= self.modules.getValues()
        for i mods.len (
          void mods[i].runEvent("onload", {})
        )
      )
    )
  )
  return __imports__954b63a69b7120a02fa83923726ef587
case "./src/rwl/ast.osl"
  class __imports__823191bbaeb9397e6eaf51c446a37c4c (
    class Parser (
      def init(string text) (
        void self.tokenise(text)
      )
      
      def parse() (
        self.pointer = 1
        
        local out @= {
          elements: self.blockContent()
        }
        
        if !self.atEnd() (
          throw "unexpected token '" ++ self.peek() ++ "'"
        )
        
        return out
      )
      
      def peek() (
        return self.tokens[self.pointer]
      )
      def consume() (
        local tkn = self.peek()
        self.pointer ++
        return tkn
      )
      def consumeWhitespace() (
        while self.peek().match("/^\\s$/") != null (
          void self.consume()
        )
      )
      def consumeSpaces() (
        local txt = ""
        while self.peek() == " " (
          txt ++= self.consume()
        )
        return txt
      )
      def atEnd() (
        return self.pointer >= self.tokens.len
      )
      def expect(string token) (
        local tkn @= self.consume()
        if tkn != token (
          throw "expected '" ++ token ++ "' got '" ++ tkn ++ "'"
        )
        return tkn
      )
      def expects(array tokens) (
        local tkn @= self.consume()
        if !tokens.contains(tkn) (
          throw "expected '" ++ tokens.join(" or ") ++ "' got '" ++ tkn ++ "'"
        )
        return tkn
      )
      
      def tokenise(string text) (
        self.tokens @= []
        local splitChars @= [
          // rwl
          "{","}",
          "[","]",
          ",",
          " ",
          "\n",
          "/","\\","*",
          "'","\"","`",
          ":","#","%","=",
          // rtr
          "(",")",
          "[","]",
          "{","}",
          ",",";",":","=",".",
          "+","-","*","/","%","^",
          "\\",
          "'","\"","`",
          " ","\n",
          "!","?"
        ]
        local buf = ""
        for i text.len (
          if splitChars.contains(text[i]) (
            if buf.len > 0 (
              void self.tokens.append(buf)
            )
            void self.tokens.append(text[i])
            buf = ""
          ) else (
            buf ++= text[i]
          )
        )
        if buf.len > 0 (
          self.tokens.append(buf)
        )
      )
      
      // statements
      def blockContent() (
        local statements @= []
        local end = false
        while !end (
          local out @= self.statement()
          if out != null (
            void statements.append(out)
          )
          void self.consumeWhitespace()
          if self.peek() == "}" or self.atEnd() (
            end = true
          ) else (
            void self.expect(",")
          )
        )
        return statements
      )
      
      def statement() (
        void self.consumeWhitespace()
        
        local tkn @= self.peek()
        
        // block
        if tkn.match("/^[a-zA-Z]+$/") != null (
          return self.blockStatement()
        )
        
        // element
        local value @= self.value()
        if value != null (
          void self.consumeWhitespace()
          local header @= self.header()
          
          return {
            data: {
              value,
              header
            },
            kind: "element"
          }
        )
      )
      
      def blockStatement() (
        local key @= self.consume()
        
        void self.consumeWhitespace()
        
        local header @= self.header()
        header.key = key
        
        void self.consumeWhitespace()
        
        if key == "script" (
          local content = ""
          local spaceContent = ""
          void self.expect("{")
          local end = false
          local depth = 1
          local stringType = null
          while !end (
            local tkn @= self.peek()
            
            if tkn == "'" (
              if stringType == "'" (
                stringType = null
              ) else if stringType == null (
                stringType = "'"
              )
            )
            
            if tkn == "\"" (
              if stringType == "\"" (
                stringType = null
              ) else if stringType == null (
                stringType = "\""
              )
            )
            
            if tkn == "`" (
              if stringType == "`" (
                stringType = null
              ) else if stringType == null (
                stringType = "`"
              )
            )
            
            if stringType == null (
              if tkn == "{" (
                depth ++
              )
              if tkn == "}" (
                depth --
              )
            )
            
            if self.atEnd() or (tkn == "}" and depth == 0 and stringType == null) (
              end = true
            ) else (
              if tkn == "\n" (
                spaceContent = ""
                void self.consume()
                if content != "" (
                  content ++= "\n"
                )
              ) else if tkn == " " (
                spaceContent ++= self.consume()
              ) else (
                content ++= spaceContent
                content ++= self.consume()
                spaceContent = ""
              )
            )
          )
          void self.expect("}")
        ) else (
          local content @= self.block()
        )
        
        return {
          kind: "block",
          data: {
            header,
            content
          }
        }
      )
      
      def block() (
        void self.expect("{")
        void self.consumeWhitespace()
        local elements @= self.blockContent()
        void self.consumeWhitespace()
        void self.expect("}")
        return { elements }
      )
      
      // header stuff
      def header() (
        local attributes @= []
        if self.peek() == "[" (
          void self.expect("[")
          local end = self.peek() == "]"
          while !end (
            local out @= self.headerItem()
            if out != null (
              void attributes.append(out)
            )
            void self.consumeWhitespace()
            if self.peek() == "," (
              void self.consume()
            )
            if self.peek() == "]" or self.atEnd() (
              end = true
            )
          )
          void self.expect("]")
        )
        
        return { attributes }
      )
      def headerItem() (
        void self.consumeWhitespace()
        local tkn @= self.consume()
        void self.consumeWhitespace()
        if self.peek() == "=" (
          if tkn.match("/^[a-zA-Z_]+$/") == null (
            throw "attribute key must consist of letters or _"
          )
          void self.consume()
          local value @= self.value()
          return {
            kind: "key",
            key: tkn,
            value
          }
        )
        if tkn.match("/^[a-zA-Z_]+$/") != null (
          return {
            kind: "flag",
            data: tkn
          }
        )
      )
      
      // values
      def value() (
        if ["'","\"","`"].contains(self.peek()) (
          return self.str()
        )
        
        if self.peek().match("/^[0-9]+$/") != null (
          return self.num()
        )
        
        if self.peek() == "#" (
          return self.color()
        )
        
        if self.peek().match("/^\\w+$/") != null (
          return self.property()
        )
      )
      
      def str() (
        local q @= self.expects(["'","\"","`"])
        local content = ""
        local end = false
        while (!end) (
          local tkn @= self.peek()
          if tkn == "\\" (
            void self.consume()
            local tkn = self.consume()
            local char = tkn[1]
            switch char (
              case "n"
                char = "\n"
                break
            )
            content ++= char ++ tkn.trim(2,-1)
            continue
          )
          
          if q == tkn (
            end = true
          ) else (
            content ++= self.consume()
          )
          if self.atEnd() (
            end = true
          )
        )
        void self.expect(q)
        return {
          type: "str",
          value: content
        }
      )
      
      // and percentage
      def num() (
        if self.peek().match("/^[0-9]+$/") == null (
          throw "unexpected token '" ++ self.peek() ++ "'"
        )
        
        local value @= self.consume().toNum()
        
        local type = "num"
        
        if self.peek() == "%" (
          void self.consume()
          type = "percentage"
        )
        
        return {
          type,
          value
        }
      )
      
      def color() (
        void self.expect("#")
        local value = self.consume()
        if ![3,6].contains(value.len) (
          throw "hex values can only be 3 or 6 characters long"
        )
        return {
          type: "color",
          value: "#" ++ value
        }
      )
      
      def property() (
        local source = self.consume()
        if source.match("/^[a-zA-Z_]+$/") == null (
          throw "source must consist of letters or _"
        )
        
        void self.consumeWhitespace()
        void self.expect(":")
        void self.consumeWhitespace()
        
        local name = self.consume()
        if name.match("/^[a-zA-Z_]+$/") == null (
          throw "name must consist of letters or _"
        )
        
        return {
          type: "property",
          source,
          name
        }
      )
    )
  )
  return __imports__823191bbaeb9397e6eaf51c446a37c4c
case "./src/rwl/debug.osl"
  class __imports__a02a29842cf0d752bb9291888afe9568 (
    def box(array area, color) (
      if !rwl.main.debug (
        return
      )
      void shared.graphics.box(area, color ?? #f00)
    )
  )
  return __imports__a02a29842cf0d752bb9291888afe9568
case "./src/rwl/value.osl"
  class __imports__b6b9b465fcf5c8c4f9d5fe786e8bce9c (
    class RWLValue (
      
    )
    
    class RWLStrValue extends RWLValue (
      type = "str"
      def init(string value) (
        self.value @= value
      )
    )
    
    class RWLNumValue extends RWLValue (
      type = "num"
      def init(number value) (
        self.value @= value
      )
    )
    
    class RWLPercentageValue extends RWLValue (
      type = "percentage"
      def init(number value) (
        self.value @= value
      )
    )
    
    class RWLColorValue extends RWLValue (
      type = "color"
      def init(string value) (
        self.value @= value
      )
    )
    
    def AstToValue(ast) (
      switch ast.type (
        case "str"
          return rwl.value.RWLStrValue(ast.value)
        case "num"
          return rwl.value.RWLNumValue(ast.value)
        case "percentage"
          return rwl.value.RWLPercentageValue(ast.value)
        case "color"
          return rwl.value.RWLColorValue(ast.value)
        case "property"
          switch ast.source (
            case "theme"
              local col = shared.theme[ast.name]
              if typeof(col) == "string" (
                return rwl.value.RWLColorValue(col)
              )
              break
            default
              return rwl.error.Error("UnknownPropertySource", {
                source: ast.source
              })
          )
          return rwl.error.Error("UnknownProperty", {
            property: ast.name,
            source: ast.source
          })
        default
          return rwl.error.Error("UnknownValueType", {
            valueType: ast.type
          })
      )
    )
  )
  return __imports__b6b9b465fcf5c8c4f9d5fe786e8bce9c
case "./src/rwl/error.osl"
  class __imports__c17b91d805bd65a760423a0b948a2c09 (
    object Errors = {
      UnknownElementKind: def(data) -> (
        return "unknown element kind:" + data.elementKind
      ),
      UnknownAttributeKind: def(data) -> (
        return "unknown attribute kind:" + data.attributeKind
      ),
      
      UnknownValueType: def(data) -> (
        return "unknown value type:" + data.valueType
      ),
      UnknownBlockType: def(data) -> (
        return "unknown block type:" + data.blockType
      ),
      
      InvalidAnchorName: def(data) -> (
        return "unknown anchor name" + data.name
      ),
      InvalidAlignmentName: def(data) -> (
        return "unknown anchor name" + data.name
      ),
      
      InvalidElemType: def(data) -> (
        return "cannot have" + data.valueType + "as element"
      ),
      
      ValueTypeMismatch: def(data) -> (
        if typeof(data.wanted) == "string" (
          data.wanted @= [data.wanted]
        )
        local forCtx = ""
        if data.attr != null (
          forCtx += "for" + data.attr
        )
        return "wanted" + data.wanted.join(" or ") + "got" + data.got ++ forCtx
      ),
      
      UnknownProperty: def(data) -> (
        return "unknown property" + data.property + "on" + data.source
      ),
      UnknownPropertySource: def(data) -> (
        return "unknown source" + data.source
      ),
      
      ElemInFrame: def(data) -> (
        return "element in a frame, only sections are allowed directly in frames"
      ),
      
      NoScriptType: def(data) -> (
        return "no script type attribute"
      ),
      InvalidScriptType: def(data) -> (
        return "unknown script type" + data.type
      ),
      InvalidScriptAttrType: def(data) -> (
        return "unknown script type attribute type" + data.type
      )
    }
    
    class Error (
      boolean isError = true
      
      def init(string type, object data) (
        self.type = type
        self.data @= data
        
        local err @= rwl.error.Errors[type]
        if err == null (
          self.txt = "unknown error type" + type
          return
        )
        self.txt = err(data)
      )
      
      def stringify() (
        return "Error<" ++ self.type ++ ">:\n" ++ self.txt.split("\n").map(l -> "    " ++ l).join("\n")
      )
    )
  )
  return __imports__c17b91d805bd65a760423a0b948a2c09
case "./src/rwl/area.osl"
  class __imports__51b19cf190821eee598f3dc8fa9be944 (
    def copy(area) (
      return [area[1], area[2], area[3], area[4]]
    )
    
    def centerX(area) (
      return area[1] + area[3] / 2
    )
    
    def centerY(area) (
      return area[2] + area[4] / 2
    )
    
    def width(area) (
      return area[3] - area[1]
    )
    
    def height(area) (
      return area[4] - area[2]
    )
  )
  return __imports__51b19cf190821eee598f3dc8fa9be944
case "./src/rwl/config.osl"
  class __imports__997006161a7103fcb97a4e4d8f4a25b1 (
    goob = "hi"
  )
  return __imports__997006161a7103fcb97a4e4d8f4a25b1
case "./src/rwl/main.osl"
  class __imports__b471b13f60903428337025313115b852 (
    debug = false
    
    class RWLNode (
      def update() (
        
      )
      
      def render() (
        
      )
      
      def expectOnHeader(string key, string type) (
        for attrI self.header.pairs.len (
          local attr @= self.header.pairs[attrI]
          if attr[2].isError (
            return attr[2]
          )
          if attr[1] == key (
            if attr[2].type != type (
              return rwl.error.Error("ValueTypeMismatch", {
                wanted: type,
                got: attr[2].type
              })
            ) else (
              return attr[2]
            )
          )
        )
        return null
      )
    )
    
    class RWLContainer extends RWLNode (
      kind = "block"
      def init(array children, object header) (
        self.children @= children
        self.header @= header
      )
      
      def update(array area, object context) (
        void self.updateBackground(area)
        
        local childArea @= rwl.area.copy(area)
        
        local childPaddingL = 0
        local childPaddingR = 0
        local childPaddingB = 0
        local childPaddingT = 0
        
        for i self.header.pairs.len (
          local pair @= self.header.pairs[i]
          local value @= pair[2]
          
          switch pair[1] (
            case "padding"
              childPaddingL = value.value
              childPaddingR = value.value
              childPaddingB = value.value
              childPaddingT = value.value
              break
            case "padding_x"
              childPaddingL = value.value
              childPaddingR = value.value
              break
            case "padding_y"
              childPaddingB = value.value
              childPaddingT = value.value
              break
            case "padding_l"
              childPaddingL = value.value
              break
            case "padding_r"
              childPaddingR = value.value
              break
            case "padding_b"
              childPaddingB = value.value
              break
            case "padding_t"
              childPaddingT = value.value
              break
          )
        )
        
        childArea[1] += childPaddingL
        childArea[2] += childPaddingB
        childArea[3] -= childPaddingR
        childArea[4] -= childPaddingT
        
        self.area @= area
        self.childArea @= childArea
        
        return rwl.main.UpdateElements(self.children, childArea, {
          x: rwl.area.centerX(childArea),
          y: rwl.area.centerY(childArea),
          anchorX: "c",
          anchorY: "c",
          color: "#fff",
          inst: context.inst
        })
      )
      
      def updateBackground(area) (
        self.area @= area
        local color @= self.expectOnHeader("color", "color")
        
        if color != null (
          self.color @= color.value
        )
        
        local allRounding @= self.expectOnHeader("rounding", "num")
        
        local rounding @= allRounding.value ?? 0
        
        local corners @= ["tl","tr","bl","br"]
        for i corners.len (
          local corner @= self.expectOnHeader("rounding_" ++ corners[i], "num")
          if corner != null (
            if typeof(rounding) != "array" (
              rounding @= []
              loop i - 1 (
                void rounding.append(allRounding.value ?? 0)
              )
            )
            void rounding.append(corner.value ?? 0)
          ) else (
            void rounding.append(allRounding.value ?? 0)
          )
        )
        self.rounding @= rounding
      )
      
      def render() (
        void self.renderBackground()
        if rwl.main.debug (
          void rwl.debug.box(self.area)
          void rwl.debug.box(self.childArea, #0f0)
        )
        void rwl.main.RenderElements(self.children)
      )
      
      def renderBackground() (
        if self.color != null (
          void shared.graphics.filledBox(self.area, self.rounding, self.color)
        )
      )
    )
    
    class RWLRoot extends RWLContainer (
      blockName = "root"
      
      def init(array children, object header) (
        self.children @= children
        self.header @= header
      )
    )
    
    class RWLFrame extends RWLContainer (
      blockName = "frame"
      
      def init(array children, object header) (
        self.children @= children
        self.header @= header
      )
      
      def update(array area, object context) (
        void self.updateBackground()
        local dir = "x"
        local flipped = false
        for flagI self.header.flags.len (
          switch self.header.flags[flagI] (
            case "Horizontal"
              dir = "x"
              break
            case "Vertical"
              dir = "y"
              break
            case "Flipped"
              flipped = !flipped
              break
          )
        )
        
        local amount = 0
        if dir == "x" (
          local total = rwl.area.width(area)
        ) else if dir == "y" (
          local total = rwl.area.height(area)
        )
        
        local frameContext @= {
          inst: context.inst
        }
        
        for i self.children.len (
          local child @= self.children[i]
          if child.kind == "block" (
            
            local childMarginL = 0
            local childMarginR = 0
            local childMarginB = 0
            local childMarginT = 0
            
            // defaults to remaining
            local size = total - amount
            local pairs @= child.header.pairs
            for pairI pairs.len (
              local pair @= pairs[pairI]
              local value @= pair[2]
              if pair[1] == "size" or (pair[1] == "width" and dir == "x") or (pair[1] == "height" and dir == "y") (
                switch value.type (
                  case "num"
                    size = value.value
                    break
                  case "percentage"
                    size = value.value / 100 * (total - amount)
                    break
                  default
                    return rwl.error.Error("ValueTypeMismatch", {
                      wanted: ["num", "percentage"],
                      got: value.type
                    })
                )
                break
              )
              
              switch pair[1] (
                case "margin"
                  childMarginL = value.value
                  childMarginR = value.value
                  childMarginB = value.value
                  childMarginT = value.value
                  break
                case "margin_x"
                  childMarginL = value.value
                  childMarginR = value.value
                  break
                case "margin_y"
                  childMarginB = value.value
                  childMarginT = value.value
                  break
                case "margin_l"
                  childMarginL = value.value
                  break
                case "margin_r"
                  childMarginR = value.value
                  break
                case "margin_b"
                  childMarginB = value.value
                  break
                case "margin_t"
                  childMarginT = value.value
                  break
              )
            )
            
            if dir == "x" (
              local childArea @= [
                (flipped ? (area[3] - amount - size) (area[1] + amount)) + childMarginL,
                area[2] + childMarginB,
                (flipped ? (area[3] - amount) (area[1] + amount + size)) - childMarginR,
                area[4] - childMarginT
              ]
            ) else if dir == "y" (
              local childArea @= [
                area[1] + childMarginL,
                (flipped ? (area[2] + amount) (area[4] - amount - size)) + childMarginB,
                area[3] - childMarginR,
                (flipped ? (area[2] + amount + size) (area[4] - amount)) - childMarginT
              ]
            )
            
            local out @= child.update(childArea, frameContext)
            if out.isError (
              return out
            )
            
            amount += size
          )
        )
      )
      
      def render() (
        void self.renderBackground()
        void rwl.main.RenderElements(self.children)
      )
    )
    
    class RWLSection extends RWLContainer (
      blockName = "section"
      
      def init(array children, object header) (
        self.children @= children
        self.header @= header
      )
    )
    
    class RWLScript extends RWLNode (
      blockName = "script"
      kind = "script"
      
      def init(body, object header, object inst) (
        self.body @= body
        self.header @= header
        
        void self.parse()
        void self.makeModule(inst)
      )
      
      def parse() (
        self.ast @= null
        // TODO: try
        local parser @= rtr.ast.Parser(body)
        self.ast @= parser.parse()
      )
      
      def makeModule(object inst) (
        if self.ast != null (
          self.mod @= rtr.main.Module(self.ast)
          void inst.rtrInst.addModule(self.mod)
        )
      )
      
      def update(array area, object context) (
        if self.mod != null (
          void self.mod.runEvent("onupdate", {})
        )
      )
    )
    
    class RWLElement extends RWLNode (
      kind = "element"
      def init(object value, object header) (
        self.value @= value
        self.header @= header
      )
      
      def update(array area, object context) (
        local text = null
        
        switch self.value.type (
          case "str"; case "num"
            text = self.value.value.toStr()
            break
        )
        
        if text == null (
          return rwl.error.Error("InvalidElemType", {
            valueType: self.value.type
          })
        )
        
        // font
        //<font>
        local size @= self.expectOnHeader("size", "num")
        if size.isError (
          return size
        )
        size = size["value"] ?? 10
        
        local spacing @= self.expectOnHeader("spacing", "num")
        if spacing.isError (
          return spacing
        )
        spacing = spacing["value"] ?? 1
        
        local line_height @= self.expectOnHeader("line_height", "num")
        if line_height.isError (
          return line_height
        )
        line_height = spacing["line_height"] ?? 1
        
        // positioning
        local padding @= self.expectOnHeader("padding", "num")
        if padding.isError (
          return padding
        )
        padding = padding["value"] ?? 0
        
        local anchor @= self.expectOnHeader("anchor", "str")
        if anchor.isError (
          return anchor
        )
        anchor = anchor["value"]
        
        if anchor != null (
          if ["tl","t","tr",
              "l","c","r",
              "bl","b","br",
              "top left", "top", "top right",
              "left", "center", "right",
              "bottom left", "bottom", "bottom right"].contains(anchor).not() (
            return rwl.error.Error("InvalidAnchorName", {
              name: anchor
            })
          )
          
          local anchorX = rwl.area.centerX(area)
          local anchorY = rwl.area.centerY(area) + (line_height * 1 * size)
          local anchorXType = "c"
          local anchorYType = "c"
          
          if anchor.endsWith("l") or anchor.endsWith("left") (
            anchorX = area[1] + padding
            anchorXType = "l"
          )
          if anchor.endsWith("r") or anchor.endsWith("right") (
            anchorX = area[3] - padding
            anchorXType = "r"
          )
          
          if anchor.startsWith("t") or anchor.endsWith("top") (
            anchorY = area[4] - padding
            anchorYType = "t"
          )
          if anchor.startsWith("b") or anchor.endsWith("bottom") (
            anchorY = area[2] + padding
            anchorYType = "b"
          )
          
          context.x = anchorX
          context.y = anchorY
          context.anchorX = anchorXType
          context.anchorY = anchorYType
        )
        
        local boxAlignment = context.anchorX
        
        local alignment @= self.expectOnHeader("alignment", "str")
        if alignment.isError (
          return alignment
        )
        alignment = alignment["value"]
        if alignment != null (
          if ["l","c","r",
              "left", "center", "right"].contains(alignment).not() (
            return rwl.error.Error("InvalidAlignmentName", {
              name: anchor
            })
          )
          
          if alignment.endsWith("l") or alignment.endsWith("left") (
            alignment = "l"
          )
          if alignment.endsWith("r") or alignment.endsWith("right") (
            alignment = "r"
          )
        )
        alignment ??= boxAlignment
        
        // appearance
        //<link>
        //<decoration>
        local color @= self.expectOnHeader("color", "color")
        if color.isError (
          return color
        )
        color = color["value"]
        if color != null (
          context.color = color
        )
        color ??= context.color
        
        local lines @= text.split("\n")
        local width = 0
        for lineI lines.len (
          width = max(width, lines[lineI].len * size * spacing)
        )
        local height = lines.len * line_height * 2 * size
        
        local x = context.x
        if boxAlignment == "l" (
          x += width / 2
        )
        if boxAlignment == "r" (
          x += width / -2
        )
        
        context.y -= height / (context.anchorY == "b" ? -2 2)
        
        self.lines @= []
        local y = context.y + (height / 2)
        for lineI lines.len (
          y -= size * line_height
          local lineW = shared.graphics.getTextWidth(lines[lineI]) * size
          local lx = x
          if alignment == "l" (
            lx += width / -2
          )
          if alignment == "r" (
            lx += width / 2 - lineW
          )
          if alignment == "c" (
            // account for char spacing
            lx += lineW * -.5
          )
          void self.lines.append([lines[lineI], lx, y, size, color])
          y -= size * line_height
        )
        
        self.debugArea @= [
          x - (width / 2),
          context.y - (height / 2),
          x + (width / 2),
          context.y + (height / 2)
        ]
        
        context.y -= height / (context.anchorY == "b" ? -2 2)
      )
      
      def render() (
        for lineI self.lines.len (
          local line @= self.lines[lineI]
          void shared.graphics.ptext(line[1], line[2], line[3], line[4], line[5])
        )
        
        void rwl.debug.box(self.debugArea ?? [])
      )
    )
    
    def UpdateElements(array elements, array area, object context) (
      for i elements.len (
        local out @= elements[i].update(area, context)
        if out.isError (
          return out
        )
      )
    )
    
    def RenderElements(array elements) (
      for i elements.len (
        //void
        void elements[i].render()
        void
      )
    )
    
    def AstToElement(ast, object inst) (
      def AstToHeader(header) (
        local flags @= []
        local pairs @= []
        for attrI header.attributes.len (
          local attr @= header.attributes[attrI]
          switch attr.kind (
            case "flag"
              flags.append(attr.data)
              break
            case "key"
              void pairs.append([attr.key, rwl.value.AstToValue(attr.value)])
              break
          )
        )
        return { flags, pairs }
      )
      //log "a" ast
      local header @= AstToHeader(ast.data.header)
      switch ast.kind (
        case "block"
          if ast.data.header.key != "script" (
            local children @= AstsToElements(ast.data.content.elements, inst)
          )
          if typeof(children) != "array" and children["isError"] (
            return children
          )
          switch ast.data.header.key (
            // top level
            case "root"
              return rwl.main.RWLRoot(children, header)
            
            // normal
            case "frame"
              return rwl.main.RWLFrame(children, header)
            case "section"
              return rwl.main.RWLSection(children, header)
            
            case "script"
              return rwl.main.RWLScript(shared.utils.removeIndents(ast.data.content), header, inst)
            
            default
              return rwl.error.Error("UnknownBlockType", {
                blockType: ast.data.header.key
              })
              break
          )
          break
        case "element"
          return rwl.main.RWLElement(rwl.value.AstToValue(ast.data.value), header)
        default
          return rwl.error.Error("UnknownElementKind", {
            elementKind: ast.kind
          })
          break
      )
    )
    
    def AstsToElements(elements, object inst) (
      local elems @= []
      for i elements.len (
        local out @= AstToElement(elements[i], inst)
        if out.isError (
          return out
        )
        void elems.append(out)
      )
      return elems
    )
    
    class RWL (
      def init(object ast, object rtrInst) (
        self.rtrInst @= rtrInst
        self.errored = false
        self.needsUpdate = true
        
        self._lastW = 0
        self._lastH = 0
        
        void self.loadFromAst(ast)
      )
      
      def loadFromAst(object ast) (
        self.ast @= ast
        self.elements @= rwl.main.AstsToElements(ast.elements, self)
        
        if typeof(self.elements) != "array" and self.elements.isError (
          error self.elements.stringify()
          self.errored = true
          return
        )
      )
      
      def update(array area) (
        local out @= rwl.main.UpdateElements(self.elements, area, {
          inst: self,
          root: true
        })
        if out.isError (
          error out.stringify()
          self.errored = true
          self.errormsg = out.stringify()
        )
      )
      
      def checkUpdate(array area) (
        local w = rwl.area.width(area)
        local h = rwl.area.height(area)
        
        // if the width or height has changed, update
        if w != self._lastW or h != self._lastH (
          self.needsUpdate = true
          
          self._lastW = w
          self._lastH = h
        )
        
        if self.needsUpdate (
          void self.update(area)
          self.needsUpdate = false
        )
      )
      
      def render(array area) (
        void self.checkUpdate(area)
      
        self.worker.area @= area
        if self.errored (
          goto rwl.area.centerX(area) rwl.area.centerY(area)
          centext "rwl errored :(" 12 : c#shared.theme.seco
          if self.errormsg != null (
            centext self.errormsg 8 : c#shared.theme.prim chy#-20
          )
          return
        )
        
        void rwl.main.RenderElements(self.elements)
      )
    )
  )
  return __imports__b471b13f60903428337025313115b852
case "./src/rtr/rwl.osl"
  class __imports__9a058537c83ea298ad90e68798b3ddc4 (
    class RTRDocument extends rtr.value.RTRValue (
      def init(object doc) (
        self.doc @= doc
      )
    
      def getRtrType() (
        return rtr.value.RTRTypeValue({
          name: "document"
        })
      )
      
      def setItem(key, value) (
        switch key.stringify() (
          case "title"
            self.doc.title = value.stringify()
            break
          case "icon"
            self.doc.icon = value.stringify()
            break
          default
            throw "unknown property" + key.stringify() + "on document"
        )
      )
    )
    
    // applies all rwl related apis to rtr
    def addToInst(object doc, object rtrInst) (
      local mods @= rtrInst.modules.getValues()
      for i mods.len (
        addToMod(doc, mods[i])
      )
    )
    
    def addToMod(object doc, object mod) (
      void mod.declScopeVar("document", RTRDocument(doc))
    )
  )
  return __imports__9a058537c83ea298ad90e68798b3ddc4
case "./src/assets/empty.rwl"
  return "  root {\n    \"grahhh\"\n  }"

case "./main.rwl"
  return "  root [color=theme:prim] {\n    frame [Horizontal] {\n      section [size=100, rounding_tr=20, rounding_br=20] {\n        \"fr\" [color=theme:seco],\n        \"str\",\n        15\n      },\n      section {\n        frame [Vertical] {\n          section [size=50] {\n            \"maybe?\" [color=theme:text, anchor=\"l\", padding=10, size=15]\n          },\n          section [color=theme:back, rounding=20, margin_r=10, margin_b=10, padding=20] {\n            \"blehhh\" [color=theme:text],\n            \"fr\",\n            \":P\" [anchor=\"bl\",size=5]\n          }\n        }\n      }\n    },\n    script {\n      event(onload) {\n        for(i, range(1,10)) {\n          log(i);\n        }\n        \n        document.title = \"gay\";\n        document.icon = \"flag-progress\";\n      }\n    }\n  }"
)
)
import as "glass" from "packages"

// import overwrite


object rwl @= {
  main: import("./src/rwl/main.osl"),
  config: import("./src/rwl/config.osl"),
  area: import("./src/rwl/area.osl"),
  error: import("./src/rwl/error.osl"),
  value: import("./src/rwl/value.osl"),
  debug: import("./src/rwl/debug.osl"),
  ast: import("./src/rwl/ast.osl")
}

object rtr @= {
  main: import("./src/rtr/main.osl"),
  ast: import("./src/rtr/ast.osl"),
  error: import("./src/rtr/error.osl"),
  value: import("./src/rtr/value.osl")
}

rtr.rwl @= import("./src/rtr/rwl.osl")

object shared @= {
  graphics: import("./src/shared/graphics.osl"),
  utils: import("./src/shared/utils.osl"),
  document: import("./src/shared/document.osl"),
  tab: import("./src/shared/tab.osl"),
  url: import("./src/shared/url.osl"),
  theme: import("./src/shared/theme.osl"),
  config: import("./src/shared/config.osl"),
  tab_worker: import("./src/shared/tab_worker.osl")
}

object layouts @= {
  shared: {
    state: import("./src/layouts/shared/state.osl"),
    utils: import("./src/layouts/shared/utils.osl"),
    topbar: import("./src/layouts/shared/topbar.osl"),
    urlbar: import("./src/layouts/shared/urlbar.osl"),
    view: import("./src/layouts/shared/view.osl")
  },
  chromeLike: {
    main: import("./src/layouts/chromeLike/main.osl"),
    topbar: import("./src/layouts/chromeLike/topbar.osl")
  },
  summit: {
    main: import("./src/layouts/summit/main.osl"),
    sidebar: import("./src/layouts/summit/sidebar.osl"),
    topbar: import("./src/layouts/summit/topbar.osl")
  }
}

object net @= {
  fetch: import("./src/net/fetch.osl"),
  response: import("./src/net/response.osl")
}

object devtools @= {
  main: import("./src/devtools/main.osl"),
  topbar: import("./src/devtools/topbar.osl"),
  
  menus: {
    elements: import("./src/devtools/menus/elements.osl"),
    console: import("./src/devtools/menus/console.osl"),
    network: import("./src/devtools/menus/network.osl"),
    storage: import("./src/devtools/menus/storage.osl")
  }
}

void layouts.chromeLike.main.init()

local document2 @= shared.document.Document()
local document @= shared.document.Document()
void document.loadUrl(shared.url.Url("local://~/projects/phosphorus/main.rwl"))
void layouts.shared.state.openTabDocument(document)
void layouts.shared.state.openTabDocument(document2)

void layouts.shared.state.selectTab(1)

void window.resize(900,500)

mainloop:
  //void layouts.chromeLike.main.update()
  void layouts.summit.main.update()
  
  // file dropping
  if file_dropped != 0 (
    local data @= open(file_dropped,[3,2,1])
    local path = data[1] ++ "/" ++ data[2] ++ data[3]
    local document @= shared.document.Document()
    void document.loadUrl(shared.url.Url("local://" ++ path))
    void layouts.shared.state.openTabDocument(document)
    file_dropped = 0
  )